/////////////////////////////////////////////////////////////
///\file J_deleteJunkWin.mel
///\brief 选点工具
///
///\author 桔
///\version 1.0
///\date  16:59 2018/3/6
///History:  
///
///////////////////////////////////////////////////////////////

global proc J_deleteJunkWin()//清理无用节点
{

string $lists[]={	"polySoftEdge","polyTweak","polyUnite",
					"polyTweak","polySplitRing","polySplit","polySoftEdge",
					"polySeparate","polyNormalPerVertex","polyNormal",
					"polyMergeVert","polyExtrudeEdge","polyDelEdge",
					"polyCollapseEdge","polyCloseBorder","polyChipOff",
					"polyBevel","polyAverageVertex","unknown","polyCylinder",
					"polySphere","polyCube","imagePlane",
					"blendShape"};
					
string $J_checkBoxs[];
if (`window -ex J_deleteJunksWin`)
	deleteUI -window J_deleteJunksWin;
	window -w 300 -h (size($lists)*2+200) -title "删除垃圾" J_deleteJunksWin;
		frameLayout -cll 1 -cl 0  -h (size($lists)*20) -l "选择要删除的项目"  J_deleteJunkWinLayOut;
		formLayout  -nd 100  -h (size($lists)*20) J_deleteJunkWinFormLayOut;
		
		$J_checkBoxs[0]=`checkBox -v 1 -onCommand "J_deleteJunksCheckBoxAllOn"
										-offCommand "J_deleteJunksCheckBoxAllOff" -label "all"`;
		formLayout -e -af $J_checkBoxs[0] top 5 -af $J_checkBoxs[0] left 5 J_deleteJunkWinFormLayOut;
		
		for($i=0;$i<size($lists);$i++)
		{
			int $leftPos=5+($i)%2*140;
			$J_checkBoxs[$i+1]=`checkBox -v 1 -label $lists[$i]`;
			formLayout -e 
				-af $J_checkBoxs[$i+1] left $leftPos
				-ac $J_checkBoxs[$i+1] top 5 $J_checkBoxs[$i-$i%2]
			J_deleteJunkWinFormLayOut;
		}
		button -l "走你" -c  "J_deleteJunks" J_deleteJunksButton;
		formLayout -e -ac J_deleteJunksButton top 5 $J_checkBoxs[size($J_checkBoxs)-1]
					  -af J_deleteJunksButton left 5
					  -af J_deleteJunksButton right 5
					  J_deleteJunkWinFormLayOut;
showWindow J_deleteJunksWin;
}

global proc J_deleteJunks()
{
	string $formCh[]=`formLayout -q -childArray J_deleteJunkWinFormLayOut`;
	
	for($j=1;$j< size($formCh);$j++)
	{
	if(`checkBox -q -ex $formCh[$j]`&&`checkBox -q -v $formCh[$j]`)
		{
			string $nodeType=`checkBox -q -l $formCh[$j]`;
			string $all[]=`ls -l -type $nodeType`;
			for($i in $all)
			{
				lockNode -l 0 $i;
				delete $i ;
			}
			print ("--------------------场景中的"+$nodeType+"节点被清除-------------------------"+"\n");
		}
	}

}

global proc J_deleteJunksCheckBoxAllOn()
{
	string $formCh[]=`formLayout -q -childArray J_deleteJunkWinFormLayOut`;
	for($i=1;$i<size($formCh);$i++)
	{
		if(`checkBox -q -ex $formCh[$i]`)
		checkBox -e -v 1 $formCh[$i];
	}

}

global proc J_deleteJunksCheckBoxAllOff()
{	string $formCh[]=`formLayout -q -childArray J_deleteJunkWinFormLayOut`;
	for($i=1;$i<size($formCh);$i++)
	{
		if(`checkBox -q -ex $formCh[$i]`)
		checkBox -e -v 0 $formCh[$i];
	}

}