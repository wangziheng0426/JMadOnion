/////////////////////////////////////////////////////////////
///\file J_modelConvert.mel
///\brief 模型格式转换
///
///\author 桔
///\version 1.0
///\date  16:56 2018/3/6
///History:  
///
///////////////////////////////////////////////////////////////

global proc J_modelConvert()//主程序
{
/////////////////////////////////////////////////////////////生成窗体
string $checkBoxs[];
int $winWidth=300;
int $count=10;
if (`window -ex checkNodesWin`)
	deleteUI -window checkNodesWin;
	window -w 300 -h ($count*2+200) -title "模型替换" checkNodesWin;
		
		frameLayout -cll 1 -cl 0 -bs "etchedIn" -h ($count*20) -l "-----------"  myColum;
		formLayout  -h ($count*20) checkNodesLayout;
		textField  -w 260 -h 25 -text "将要替换的模型名字复制到这里（不要带前后缀）" objToRebuild;

		intSliderGrp -label "随机旋转" -field true
			-minValue 0 -maxValue 180
			-fieldMinValue 0 -fieldMaxValue 180
			-value 0 objRR;
		intSliderGrp -label "随机缩放" -field true
			-minValue 1 -maxValue 1000
			-fieldMinValue 1 -fieldMaxValue 1000
			-value 1 objRS;
		intSliderGrp -label "随机位移" -field true
			-minValue 0 -maxValue 1000
			-fieldMinValue 0 -fieldMaxValue 1000
			-value 0 objRP;
		separator  -w $winWidth -bgc .5 .5 .5 -st "in"  sepx1 ;	
		textScrollList -h 100 J_replaceList;
		//textField  -w 260 -h 25 pathText;
		button -l "读取模型" -c  "addModelsFromSelectC" buttonRead ;
		button -l "替换模型" -c  "runRebuildWorld" buttonRun ;
	/////////////////////////////////////////////////////////编辑窗体	
	int $apx=0;
	int $apy=0;

	formLayout -e 
	
		-af objToRebuild left 0 
		-af objToRebuild top 0
		-af objToRebuild right 0

		
		-af objRR left -50 
		-ac objRR top $apy objToRebuild
		
		-af objRS left -50 
		-ac objRS top $apy objRR
		
		-af objRP left -50 
		-ac objRP top $apy objRS
		
		-af sepx1 left 0 
		-ac sepx1 top $apy objRP
		-af sepx1 right 0 

		-af J_replaceList left $apx 
		-ac J_replaceList top $apy sepx1
		-af J_replaceList right $apx
		
		-af buttonRead left $apx 
		-ac buttonRead top $apy J_replaceList
		-af buttonRead right $apx
		
		-af buttonRun left $apx 
		-ac buttonRun top $apy buttonRead
		-af buttonRun right $apx
		
		checkNodesLayout;
showWindow checkNodesWin;
}

global proc runRebuildWorld()
{
	string $temp[]=`textScrollList -q -ai J_replaceList`;

	string 	$orgObj=`textField -q -tx objToRebuild`;
	int $randPos=`intSliderGrp -q -v objRP`;
	int $randRot=`intSliderGrp -q -v objRR`;
	int $randSca=`intSliderGrp -q -v objRS`;
	select ($orgObj+"*");
	string $orgObjs[]=`ls  -l -sl`;
	for($xx=0;$xx<size($orgObjs);$xx++)
	{
		int $randC=rand(0,size($temp));
		if(`objExists $orgObjs[$xx]`)
		{
		if(`objectType $orgObjs[$xx]`=="transform")
		{
		vector $pos=`xform -q -ws -rp $orgObjs[$xx]`;
		vector $rot=`xform -q -ws -ro $orgObjs[$xx]`;
		vector $scale=`xform -q -ws -s $orgObjs[$xx]`;
		string $par[]=`listRelatives -p $orgObjs[$xx]`;
		if($temp[$randC]!="")
		{
			string $t[]=`duplicate $temp[$randC]`;
			
			setAttr ($t[0]+".translateX") ($pos.x);
			float $tempa=(($pos.y)+(rand(($randPos*(-1)),$randPos)));
			setAttr ($t[0]+".translateY") $tempa;
			setAttr ($t[0]+".translateZ") ($pos.z);
			
			setAttr ($t[0]+".rotateX")  ($rot.x);
			
			$tempa=($rot.y)+rand(($randRot*(-1)),$randRot );
			setAttr ($t[0]+".rotateY")  $tempa;
			setAttr ($t[0]+".rotateZ")  ($rot.z);
			
			$tempa=(rand(1,$randSca));
			float $rr=$tempa+($scale.x);
			setAttr ($t[0]+".scaleX") $rr;
			$rr=$tempa+($scale.y);
			setAttr ($t[0]+".scaleY") $rr;
			$rr=$tempa+($scale.z);
			setAttr ($t[0]+".scaleZ") $rr;
			if($par[0]!="")
			{parent $t[0] $par[0];}
			delete $orgObjs[$xx];
		}
		}
		}
	}
	
}
global proc addModelsFromSelectC()
{
	string $sel[]=`ls -sl`;
	textScrollList -e -ra  J_replaceList;
	for($i in $sel)
	{
	textScrollList -e -append $i J_replaceList;
	}
}

