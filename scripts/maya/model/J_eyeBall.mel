/////////////////////////////////////////////////////////////
///\file J_eyeBall.mel
///\brief 导入眼球工具
///
///\author 桔
///\version 1.0
///\date  16:34 2018/3/6
///History:  
///
/////////////////////////////////////////////////////////////////
global proc J_eyeBall()
{    
    string $path=`J_getFilePath`;
    string  $ss[]={"删除历史","从新导入","关闭"};
    int $ebe=0;
    if(`attributeExists "eyeBallExist" lambert1`)
    {
        $ebe=`getAttr lambert1.eyeBallExist`;
    }
    else
    {
    addAttr -ln "eyeBallExist"  -at double -dv 0 lambert1;
    setAttr -e-keyable true lambert1.eyeBallExist;
    }
    if($ebe>0)
    {
		string $res=`confirmDialog -title "眼球已存在，且未清除历史" -message "是否需要删除眼球历史？或者从新导入眼球"
		-button $ss[0] -button $ss[1] -button $ss[2] -defaultButton $ss[0]
		-cancelButton $ss[2] -dismissString $ss[2]`;
		if($res==$ss[0])
			{print "删除历史";deleteHNew;}
		else if($res==$ss[1])
			{print "从新导入";reimEye;imEye($path);loadPresets;}
    }
    else
    {
        imEye($path);loadPresets;setAttr lambert1.eyeBallExist 1;savePresetsNew;
		
	}

		
}				
global proc imEye(string $path)
{	
	string $EyeBallFile[]=`getFileList -folder ($path+"eyeball/")  -filespec "*.mb"`;
    string $sysPath=($path+"eyeball/") +$EyeBallFile[0];
    $fileToSource="file -i  \""+$sysPath+"\"";
		eval $fileToSource ;
}

global proc deleteHNew()
{
    savePresetsNew();J_duplicateMorphTarget();J_breakSelectObjectConnections();J_getKeys;DeleteAllExpressions;DeleteAllHistory;catch(`select "*eye_grp*"`);makeIdentity -apply true -t 1 -r 1 -s 1 -n 0;   FreezeTransformations ;
}
global proc reimEye()
{
	catch(`select "*eye_grp*"`);
	string $sel[]=`ls -sl`;
	for($item in $sel)
	{
	if(`objExists $item`)
		delete $item;
	}
}

global proc savePresetsNew()
{
	string $attrs[]={"scaleX","scaleY","scaleZ","translateX","translateY","translateZ","rotateX",
	"rotateY","PupilOuter","PupilInner","HighlightBg","HighlightInOt","HighlightCenterRx",
	"HighlightCenterRy","HighlightLocalRx","HighlightLocalRy"};
	catch(`select "*eye_grp*"`);
	string $sel[]=`ls -sl`;
	for($f in $sel)
	{
		for($n in $attrs)
		{
		if(!`attributeExists $n lambert1`)
			{
			addAttr -ln $n  -at double  -dv 0 lambert1;
			setAttr -e-keyable true ("lambert1."+$n);
			}
		if(`attributeExists $n $f`)
			{
			float $temp=`getAttr ($f+"."+$n)`;
			setAttr ("lambert1."+$n) $temp;
			}
		}
		
	}
}
global proc loadPresets()
{
	string $attrs[]={"scaleX","scaleY","scaleZ","translateX","translateY","translateZ","rotateX",
	"rotateY","rotateZ","PupilOuter","PupilInner","HighlightBg","HighlightInOt","HighlightCenterRx",
	"HighlightCenterRy","HighlightLocalRx","HighlightLocalRy"};
	select "*l_eye_grp*";
	string $sel[]=`ls -sl`;
	for($f in $sel)
	{
		for($n in $attrs)
		{
		if(`attributeExists $n "lambert1" `)
			{
			float $temp=`getAttr ("lambert1."+$n)`;
			setAttr ($f+"."+$n) $temp;
			}
		}
		
	}
}

global proc string[] J_eyeBallDiGui(string $a,string $allNodes[])//$a检查对象  $allNodes[]反馈数组
    {
			
    	 	string $chs[]=`listRelatives -f -c -type transform $a`;
    		if(size($chs))
    		{
    			for($k in $chs)
    			{
					if($k!="")
						{
							int $count=size($allNodes);
							$allNodes[$count]= $k;
							J_eyeBallDiGui($k,$allNodes);
						}
    			}
    		}
    		else
    		return {""};
    		return $allNodes;
    	
    }
global proc J_breakSelectObjectConnections()//打断所选物体的链接
{
	select "*_eye_grp*";
    string $allTransform[]=`ls -l -sl -type transform`;
	J_unLockAttrs($allTransform);
    string $eyeGrpChTemp[];
    int $allMeshTranCount=0;
	for($item in $allTransform)
        {		
			string $temp[];
			string $tempA[]=J_eyeBallDiGui($item,$temp);
			appendStringArray($eyeGrpChTemp,$tempA,size($tempA));
		}
	$eyeGrpChTemp=stringArrayRemoveDuplicates($eyeGrpChTemp);
	print $eyeGrpChTemp;
		for($item in $eyeGrpChTemp)
		{
			if(`objExists $item`)
			{
				J_breakConnections($item);
				if(`attributeExists "ToDelete" $item`)
				delete $item;
			}
		}

}



global proc J_duplicateMorphTarget()
{
    
    string $path=`J_getFilePath`;
	string $settingFile =$path+"eyeball/attr.txt";
	$fileId=`fopen $settingFile "r"`;
	string $getwordTemp[];
	int $getwordTempId=0;
	do
	{
		$getwordTemp[$getwordTempId]=`fgetword $fileId`;
		$getwordTempId++;	
		
	}
	while(size( $getwordTemp[$getwordTempId-1] ) > 0 );
	fclose $fileId;
	int $segment=int($getwordTemp[1]);
	string $targetName=$getwordTemp[3];
	
	float $segmentRowInner[];
	float $segmentRowOuter[];
	$segmentRowInner[0]=-1;
	$segmentRowOuter[0]=-1;
	
	$segmentRowInner[$segment-1]=1;
	$segmentRowOuter[$segment-1]=1;
	
	$segmentRowInner[int($segment/2)]=`getAttr l_eye_grp.PupilInner`;
	float $segmentRowInnerDef=linstep(-1,1,$segmentRowInner[int($segment/2)]);
		
	$segmentRowOuter[int($segment/2)]=`getAttr l_eye_grp.PupilOuter`;
	float $segmentRowOuterDef=linstep(-1,1,$segmentRowOuter[int($segment/2)]);
	
	for($i=1;$i<int($segment/2);$i++)//左侧数据
	{
		$segmentRowInner[$i]=linstep(0,(int($segment/2)),$i)*($segmentRowInner[int($segment/2)]-$segmentRowInner[0])+$segmentRowInner[0];
		$segmentRowOuter[$i]=linstep(0,(int($segment/2)),$i)*($segmentRowOuter[int($segment/2)]-$segmentRowOuter[0])+$segmentRowOuter[0];

	}
	for($i=int($segment/2+1);$i<$segment-1;$i++)//右侧数据
	{
		$segmentRowInner[$i]=linstep(int($segment/2),($segment-1),$i)*($segmentRowInner[$segment-1]-$segmentRowInner[int($segment/2)])+$segmentRowInner[int($segment/2)];
		$segmentRowOuter[$i]=linstep(int($segment/2),($segment-1),$i)*($segmentRowOuter[$segment-1]-$segmentRowOuter[int($segment/2)])+$segmentRowOuter[int($segment/2)];

	}
	//print $segmentRowInner;print "\n";
	//print $segmentRowOuter;print "\n";
	string $L_eye_Pupil=`createNode -n "L_eye_grp_Pupil" transform`;
	string $R_eye_Pupil=`createNode -n "R_eye_grp_Pupil" transform`;
	setAttr L_eye_grp_Pupil.visibility 0;
	setAttr R_eye_grp_Pupil.visibility 0;
	for($i=0;$i<$segment;$i++)
	{
		for($j=0;$j<$segment;$j++)
		{
			setAttr "l_eye_grp.PupilOuter" ($segmentRowOuter[$i]);
			setAttr "l_eye_grp.PupilInner" ($segmentRowInner[$j]);
			string $meshTemp[]=`duplicate l_pupil`;
			string $numTempI=J_addZero(3,(int(100/($segment-1)))*$i);
			string $numTempJ=J_addZero(3,(int(100/($segment-1)))*$j);
			string $meshName="L_"+$targetName+"_Out"+$numTempI+"_Inner_B_"+$numTempJ;

			rename $meshTemp[0] $meshName;
			parent $meshName $L_eye_Pupil;
			
			string $meshTemp1[]=`duplicate r_pupil`;
			string $meshName1="R_"+$targetName+"_Out"+$numTempI+"_Inner_B_"+$numTempJ;

			rename $meshTemp1[0] $meshName1;
			parent $meshName1 $R_eye_Pupil;
		}
	}
	setAttr "l_eye_grp.PupilOuter" ($segmentRowOuter[int($segment/2)]);
	setAttr "l_eye_grp.PupilInner" ($segmentRowInner[int($segment/2)]);
	
	
}
	