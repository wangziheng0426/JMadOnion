/////////////////////////////////////////////////////////////
///\file animationExporter.mel
///\brief fbx导出工具
///
///\author 桔
///\version 1.4.2
///\date  9:45 2022/4/6
///History:  导出动画工具
///
///////////////////////////////////////////////////////////////
global proc J_resourceExporter()//主窗口
{
    if (`window -ex J_resourceExporterWin`)
        deleteUI -window J_resourceExporterWin;
    window -w 300 -h 800 -title "资源导出" J_resourceExporterWin;
    //文件导出设置
    string $geoSetting[]={"SmoothingGroup","SmoothMesh","SelectionSets","Triangulate",
     "IncludeChildren","InputConnections","Skin","BlendShape" };
    string $geoSettingCheckBox[];
    formLayout  -numberOfDivisions 100  J_resourceExporter_FromLayout0;
        frameLayout -cll 1 -cl 0 -l "模型选项"  J_resourceExporter_rigframeLayout;
            string $formLayout_Temp0=`formLayout -numberOfDivisions 100 `;
                for ($iItem=0;$iItem<size($geoSetting);$iItem++)
                {
                    $geoSettingCheckBox[$iItem]=`checkBox -label  $geoSetting[$iItem] `;
                    formLayout -e 
                    -ap $geoSettingCheckBox[$iItem] left 0 (1+50*($iItem%2))
                    -ap $geoSettingCheckBox[$iItem] right 0 (49+50*($iItem%2))
                    -af $geoSettingCheckBox[$iItem] top (15*floor($iItem/2)) $formLayout_Temp0;
                }
            setParent ..;
            button -l "导出绑定" -c  "J_resourceExporterExportRig" J_expRig;   
            separator -style "in"; 
        setParent ..;
        frameLayout -cll 1 -cl 0 -l "动画选项"  J_resourceExporter_aniframeLayout;
            formLayout  -numberOfDivisions 100  J_resourceExporter_animationFromLayout;
                textScrollList -h 80 -sc "J_resourceExporterSelitem"  -allowMultiSelection 1  J_allCameraNode;
                textScrollList  -sc "" -allowMultiSelection 1 J_allRefNode;
                
                checkBox -label "曲线平滑" -v 1 J_animationExporter_chbox01;
                checkBox -label "曲线打平"  J_animationExporter_chbox02;
                checkBox -label "根骨归零"  J_animationExporter_chbox03;

                checkBox -label "相机->fbx" -v 1 J_animationExporter_chbox04;
                checkBox -label "动画->fbx"  J_animationExporter_chbox05;

                optionMenu -label "差值模式" J_animationExporter_optionMenu01;
                menuItem  -label "resample";
                menuItem  -label "euler";
                menuItem  -label "quaternion";

                button -l "导出相机" -c  "J_resourceExporterExportCam" J_expCam;
                button -l "导出动画" -c  "" J_expAni;
                button -l "批量导" -c  "" J_expAniBat;
            setParent ..;
        setParent ..;
       
    formLayout -e 
        -ap J_resourceExporter_rigframeLayout left 0 1
        -ap J_resourceExporter_rigframeLayout right 0 99
        -af J_resourceExporter_rigframeLayout top 0
        
        -ap J_resourceExporter_aniframeLayout left 0 1
        -ap J_resourceExporter_aniframeLayout right 0 99
        -ac J_resourceExporter_aniframeLayout top 1 J_resourceExporter_rigframeLayout
        -af J_resourceExporter_aniframeLayout bottom 0

    J_resourceExporter_FromLayout0;

    formLayout -e        
        
        -ap J_allCameraNode left 1 1
        -ap J_allCameraNode right 1 99
        -af J_allCameraNode top  1 

        -ap J_allRefNode left 1 1
        -ap J_allRefNode right 1 99
        -ac J_allRefNode top 1 J_allCameraNode
        -ap J_allRefNode bottom 90 99
        
        -ap J_animationExporter_chbox01 left 0 1
        -ap J_animationExporter_chbox01 right 0 33
        -ac J_animationExporter_chbox01 top  2 J_allRefNode

        -ap J_animationExporter_chbox02 left 0 34
        -ap J_animationExporter_chbox02 right 0 66
        -ac J_animationExporter_chbox02 top  2 J_allRefNode

        -ap J_animationExporter_chbox03 left 0 67
        -ap J_animationExporter_chbox03 right 0 99
        -ac J_animationExporter_chbox03 top  2 J_allRefNode

        -ap J_animationExporter_optionMenu01 left 0 1
        -ap J_animationExporter_optionMenu01 right 0 99
        -ac J_animationExporter_optionMenu01 top  2 J_animationExporter_chbox03

        -ap J_animationExporter_chbox04 left 0 1
        -ap J_animationExporter_chbox04 right 0 33
        -ac J_animationExporter_chbox04 top  2 J_animationExporter_optionMenu01

        -ap J_animationExporter_chbox05 left 0 34
        -ap J_animationExporter_chbox05 right 0 66
        -ac J_animationExporter_chbox05 top  2 J_animationExporter_optionMenu01        

        -ap J_expCam left 0 1
        -ap J_expCam right 0 33
        -ac J_expCam top  2 J_animationExporter_chbox05

        -ap J_expAni left 0 34
        -ap J_expAni right 0 66
        -ac J_expAni top 2 J_animationExporter_chbox05        

        -ap J_expAniBat left 0 67
        -ap J_expAniBat right 0 99
        -ac J_expAniBat top 2 J_animationExporter_chbox05
       
    J_resourceExporter_animationFromLayout;


    
    showWindow J_resourceExporterWin;
    J_resourceExporterInit();
    J_resourceExporterRunScriptJob();
    python("JpyModules.public.J_deleteUnknownNode()");
    python("JpyModules.public.J_cleanVaccine_gene()");
}

global proc J_resourceExporterInit()
{
    string $allCamera[]=`ls -type "camera"`;
    string $allCamTr[]=`listRelatives  -fullPath -p $allCamera`;
    string $sysCam[]={"|back","|front","|left","|persp","|side","|top"};
    textScrollList  -e -ra  J_allCameraNode;
    textScrollList  -e -ra   J_allRefNode;
    for ($item0 in $allCamTr)
    {
        int $temp=0;
        for ($item1 in $sysCam)
        {
            if ($item0==$item1)$temp=1;
        }
        if ($temp==0)
        textScrollList  -e -a $item0  J_allCameraNode;
    }
    string $allRefFile[]=`file -q -r`;
    for ($item0 in $allRefFile)
    {   
        string $refNode=`referenceQuery -referenceNode $item0`;

        textScrollList  -e -a $refNode  J_allRefNode;
    }
}


global proc J_resourceExporterExportCam()
{
    string $selCam[]=`textScrollList  -q -si  J_allCameraNode`;
    python("import os");
    string $outPath=python("os.path.dirname(maya.cmds.file(query=True,sceneName=True))")+"/cache/";
    if (size ($outPath)<3)
        $outPath= "c:/temp";
    if (size($selCam)>0)
    {
        for ($item in $selCam)
        {
            if (`checkBox -q -v J_animationExporter_chbox04`==1)
            {
                python("JpyModules.pipline.J_resourceExporter.J_animationExportCamera2Fbx('"+$item+"')"); 
            }
            else
            {
                python("JpyModules.pipline.J_resourceExporter.J_animationExportCamera2Abc('"+$item+"')");    
            }
        }
        string $cmd="os.startfile(\""+$outPath+"\")";
        python($cmd); 
    }
    
}
//J_resourceExporterExportCam("pSphere1","Z:/THD/file.fbx")

//查文件路径,未保存,则以c:/temp为准
global proc string J_resourceExporterGetFilePath()
{       
    python("import os");
    string $filePath=python("os.path.dirname(maya.cmds.file(query=True,sceneName=True))")+"/";
    if (size ($filePath)<3)
        return "c:/temp/";
    return $filePath;
}

//随选择修改窗体内容
global proc J_resourceExporterRunScriptJob()
{
    int $sjId = `scriptJob -e "SelectionChanged" J_resourceExporterScriptJob `;
    string $temp = ("scriptJob -k "+ $sjId);
    scriptJob -uid "J_resourceExporterWin" $temp;

}
global proc J_resourceExporterScriptJob()
{
    J_resourceExporterInit();
    textScrollList -e -deselectAll  J_allCameraNode;
    string $allListCam[]=`textScrollList  -q -ai  J_allCameraNode`;
    string $sel[]=`ls -sl -allPaths -l`;
    for($i in $sel)
    {
        if(stringArrayContains( $i, $allListCam))
        textScrollList -e -si $i J_allCameraNode;
    }
}
/////////
global proc J_resourceExporterSelitem()
{
    string $allListCam[]=`textScrollList  -q -si  J_allCameraNode`;
    select $allListCam;
}
//导出动画
global proc J_resourceExporterExportAni(string $refNode,string $outPath)
{
    int $startFrame=`playbackOptions -query -minTime`;
    int $endFrame=`playbackOptions -query -maxTime`+1;
    if (`objExists $refNode`)
    {
        string $refFile=`referenceQuery -filename $refNode`;
        string $root[]={};
        if (!`referenceQuery -isLoaded $refNode`)
        {
            file -loadReferenceDepth "asPrefs" -loadReference $refNode $refFile;
        }
        //搜索根骨
        $root=J_resourceExporterGetRootJoint($refNode);
        //搜索根骨
        
        //导出所选
        if (!`objExists $root[0]`)
            $root=`ls -type joint "Bone_Broom_01"`;
        if (size($root)<1){return;}
        
        bakeResults -t ($startFrame+":"+$endFrame)  -hierarchy "below" -simulation true 
            -sampleBy 1 -oversamplingRate 1 -disableImplicitControl true -preserveOutsideKeys true 
            -sparseAnimCurveBake false -removeBakedAttributeFromLayer false -removeBakedAnimFromLayer true 
            -bakeOnOverrideLayer false -minimizeRotation true -controlPoints false -shape true 
            $root;
        
        //导入ref
        file -importReference $refFile;
        //卸载其他ref
        string $allRefFile[]=`file -q -r`;
        for ($item1 in $allRefFile)
        {   
            string $refNode1=`referenceQuery -referenceNode $item1`;
            if ($refNode1!=$refNode)
            {
                if (`referenceQuery -isLoaded $refNode1`)
                {
                    file -unloadReference $refNode1 $item1;
                }
            }
        }
        
        if (`objExists "|Bone_Broom_01"`) delete "|Bone_Broom_01";
        //骨骼移到最外侧
        string $newRoot[];
        for ($item in $root)
        {
            if (`objExists $item`)
            {
                string $parentTemp[]=`listRelatives -p $item`;
                if (size($parentTemp)>0)
                {
                    string $temp[]= `parent -w $item`;
                    for ($itemTemp in $temp)
                    {
                        $newRoot[size($newRoot)]=$itemTemp;
                    }
                }
            }
        }
        if (size ($newRoot)<1)
        {
            print "找不到骨骼";
            return ;
        }
        select $newRoot;
        //关闭约束
        delete `ls -type constraint`;
        //删除名字空间
        J_resourceExporterRemoveAllNameSpace();
        //羽化动画曲线
        for ($item2 in `ls -type joint`)
        {
            filterCurve ($item2+".rotateX");
            filterCurve ($item2+".rotateY");
            filterCurve ($item2+".rotateZ");
        }
        //导出动画
        FBXResetExport ;
        FBXExportInAscii  -v true;
        string $takeName= `basenameEx $refFile`;
        FBXExportBakeComplexAnimation -v 1;    
        FBXExportShapes -v true;
        FBXExportBakeComplexStart -v $startFrame;
        FBXExportBakeComplexEnd -v $endFrame;
        FBXExportBakeResampleAnimation -v 1;
        FBXExportInAscii -v 1;
        FBXExportIncludeChildren -v 1;
        FBXExportSplitAnimationIntoTakes -clear;    
        FBXExportDeleteOriginalTakeOnSplitAnimation -v true;
        FBXExportSplitAnimationIntoTakes -v $takeName $startFrame $endFrame;
        string $aniOut=$outPath+".fbx";
        FBXExport -f $aniOut -s ;
        //导出表情
        string $faceModels[]=`ls "*_Face"`;
        string $faceModelsHis[]= `listHistory $faceModels`;
        delete `ls -type skinCluster $faceModelsHis`;
        for ($faceItem in $faceModels)
        {
            string $hisNodes[]=`listHistory $faceItem `;
            string $blendNodes[]=`ls -type blendShape $hisNodes`;
            if (size($blendNodes)>0 )
            {    setKeyframe -t $startFrame $blendNodes ;
                setKeyframe -t $endFrame $blendNodes ;
                bakeResults -t ($startFrame+":"+$endFrame) -simulation true $blendNodes;
                parent -w $faceItem;
                string $facOut=$outPath+"face.fbx";
                FBXExportSplitAnimationIntoTakes -clear;    
                FBXExportDeleteOriginalTakeOnSplitAnimation -v true;
                FBXExport -f $facOut -s ;
                python("JpyModules.animation.J_resourceExporter.J_replaceSubdeformer(\""+$facOut+"\")");
            }
        }
        
    }
}
global proc J_resourceExporterExportAniBut()
{
    string $selRef[]=`textScrollList  -q -si  J_allRefNode`;
    string $mayafile=`file -q -sceneName`;
    string $outPathA=J_resourceExporterGetFilePath();
    if (size($selRef)>0)
    {
        for ($item in $selRef)
        {
            string $refFilename=`referenceQuery -filename $item`;
            string $outPath=J_resourceExporterGetFilePath()+
                python("JpyModules.animation.J_resourceExporter.J_getFileName()") +"_"+$item;
            J_resourceExporterExportAni($item,$outPath);
            file -f  -prompt false   -open $mayafile;
        }
    }
    //导出后打开文件夹
    string $cmd="os.startfile(os.path.dirname(\""+$outPathA+"\"))";
    python($cmd); 
    
}
global proc J_resourceExporterExportBatBut(int $state)
{
    string $file[]=`fileDialog2 -fileMode 2 `;
    string $mayaFiles[]=`getFileList -folder $file[0] -filespec "*.m?" `;
    string $filter=`textField -q -text J_tex`;

    for ($item in $mayaFiles)
    {
        if (`endsWith $item ".mb"` ||`endsWith $item ".ma"` )
        {
            file -f  -prompt false   -open ($file[0]+"/"+$item );
            string $allCamera[]=`ls -type "camera"`;
            string $allCamTr[]=`listRelatives  -fullPath -p $allCamera`;
            string $sysCam[]={"|back","|front","|left","|persp","|side","|top"};
            
            for ($item0 in $allCamTr)
            {
                string $outPath=J_resourceExporterGetFilePath();
                int $temp=0;
                for ($item1 in $sysCam)
                {
                    if ($item0==$item1)$temp=1;
                }
                if ($temp==0)
                {
                string $buffer[];
                $numTokens = `tokenize $item0 "|" $buffer`;
                
                string $buffer1[];
                $numTokens = `tokenize $buffer[size($buffer)-1] ":" $buffer1`;
                
                $outPath+=$buffer1[size($buffer1)-1] +".fbx";
                //J_resourceExporterExportCam($item0,$outPath);
                }
            }
            string $allRefFile[]=`file -q -r`;
            if ($state==1)
            {
                for ($item0 in $allRefFile)
                {   
                    if ($filter=="")
                    {
                        if (`file -q -ex $item0`)
                        {
                            string $refNode=`referenceQuery -referenceNode $item0`;
                            string $outPath=J_resourceExporterGetFilePath()+
                                python("JpyModules.animation.J_resourceExporter.J_getFileName()") +"_"+$refNode ;
                            J_resourceExporterExportAni($refNode,$outPath);
                            
                            file -f -prompt false   -open ($file[0]+"/"+$item );
                        }
                    }
                    else{
                        if (`file -q -ex $item0`)
                        {                        
                            string $refNode=`referenceQuery -referenceNode $item0`;
                            if(`gmatch $refNode $filter`)
                            {
                            string $outPath=J_resourceExporterGetFilePath()+
                                python("JpyModules.animation.J_resourceExporter.J_getFileName()") +"_"+$refNode ;
                            J_resourceExporterExportAni($refNode,$outPath);
                            
                            file -f -prompt false   -open ($file[0]+"/"+$item );
                            }
                        }
                    }
                }
            }
        }
    }
}
global proc  J_resourceExporterRemoveAllNameSpace()
{
    string $nameSpaces[]=`namespaceInfo -listOnlyNamespaces`;
    string $items[] = { "shared", "UI"};
    string $diff[] = stringArrayRemove($items, $nameSpaces);
    if(size($diff)>0)
    {
        for($i in $diff)
        {
            namespace -mergeNamespaceWithRoot -removeNamespace $i ;
            print ($i +"被删除\n");
        }
        J_resourceExporterRemoveAllNameSpace();
    }
}
global proc string[] J_resourceExporterGetRootJoint(string $refNode)
{
    string $rootJoint="";
    string $allNodes[]= `referenceQuery -nodes  $refNode `;
    if (size($allNodes)<1) return {""};
    string $allSkinClustersFromRef[]=`ls -type skinCluster  -allPaths $allNodes`;
    if (size($allSkinClustersFromRef)<1) return {""};
    string $skinClustersHis[]=`listHistory $allSkinClustersFromRef`;
    if (size($skinClustersHis)<1) return {""};
    string $allJointFromSkinClusters[]=`ls -long -type joint $skinClustersHis`;
    if (size($allJointFromSkinClusters)<1) return {""};
    string $refNamespace=`referenceQuery -namespace $refNode`;
    $refNamespace=`substring $refNamespace 2 (size($refNamespace))`+":";
    string $res[];
    int $jointCount=0;
    if (size($allJointFromSkinClusters)>0)
    {   
        for ($itemSC in $allJointFromSkinClusters)
        {
            $rootJoint=$itemSC ;
            string $par[]=`listRelatives -p -f $itemSC`;
            if (size($par)>0)
            {
                while (`objectType $par[0]`!="transform")
                {
                    $rootJoint=$par[0];
                    $par=`listRelatives -p -f $par[0]`;
                }
            }
            $res[$jointCount]=$rootJoint;
            $jointCount++;
        }
    }
    //$rootJoint=python("\""+$rootJoint+"\".replace("+"\""+$refNamespace+"\",\"\")");
    return stringArrayRemoveDuplicates($res);
}


J_resourceExporter;