/////////////////////////////////////////////////////////////
///\file J_nClothTool.mel
///
///\brief    布料制作工具  
///
///\author 桔
///\version 1.0
///\date  18:50 2018/12/4
///History:  
///
///////////////////////////////////////////////////////////////
global proc J_nClothTool()//主程序
{
/////////////////////////////////////////////////////////////生成窗体
if (`window -ex J_nClothTool`)
    deleteUI -window J_nClothTool;
    window -w 300 -h 400 -title "nCloth工具" J_nClothTool;
        int $ButtonW=40;
        int $ButtonH=30;
        columnLayout -adjustableColumn 1 -cal "center" J_columLayout;
        frameLayout -cll 1 -cl 0 -l "创建布料"  J_clothFrameLayout;
            formLayout  -numberOfDivisions 100  J_clothFormLayout;
                string $cbu01 =`button -al "center"  -w $ButtonW -h $ButtonH -l "创建布料"  -c J_createNCloth`;
                string $cbu02 =`button -al "center"  -w $ButtonW -h $ButtonH -l "创建碰撞"  -c J_createPassive`;
                string $cbu03 =`button -al "center"  -w $ButtonW -h $ButtonH -l "取消碰撞"  -c J_diableCollider`;
                string $cbu04 =`button -al "center"  -w $ButtonW -h $ButtonH -l "选择元素"  -c J_selectMember`;
                string $cbu05 =`button -al "center"  -w $ButtonW -h $ButtonH -l "点面约束"  -c J_pointToSuf`;
                string $cbu06 =`button -al "center"  -w $ButtonW -h $ButtonH -l "元素约束"  -c J_pointTopoint`;
                string $cbu07 =`button -al "center"  -w $ButtonW -h $ButtonH -l "输入吸附"  -c J_paintInputAttract`;
                string $cbu08 =`button -al "center"  -w $ButtonW -h $ButtonH -l "碰撞强度"  -c J_paintCollideStrength`;
                
            setParent ..;
        setParent ..;
        ///////////////////////////布料
        frameLayout -cll 1 -cl 0 -l "导入导出"  J_clothInOutFrameLayout;
            formLayout  -numberOfDivisions 100  J_clothInOutFormLayout;
                //textField -tx "D:/SM/ncloth_rig" -width 300 outPutTextField;
                //symbolButton -width 20 -image "navButtonBrowse.xpm"   -command J_inOutClothInfoPath browsBtn;
                string $cbu09 =`button -al "center"  -w $ButtonW -h $ButtonH -l "导出布料设置"  -c J_exportNcloth`;
                string $cbu10 =`button -al "center"  -w $ButtonW -h $ButtonH -l "导入布料设置"  -c J_importNcloth`;
            setParent ..;
        setParent ..;
        ///////////////////////////布料
        frameLayout -cll 1 -cl 0 -l "导入布料预设"  J_clothPresetsFrameLayout;
            formLayout  -numberOfDivisions 100  J_clothPresetsFormLayout;
                textScrollList -ams 1 -w 180 -h 200 nclothPresetslist;
                textScrollList -ams 1 -w 180 -h 200 -sc "J_nClothTool_selectitem()" nclothNodeslist;
                string $cbu11 =`button -al "center"  -w $ButtonW -h $ButtonH -l "读取预设"  -c J_nClothTool_readPresets`;
                string $cbu12 =`button -al "center"  -w $ButtonW -h $ButtonH -l "保存预设"  -c J_nClothTool_savePresets`;
                string $cbu13 =`button -al "center"  -w $ButtonW -h $ButtonH -l "删除预设"  -c J_nClothTool_delPresets`;
                string $cbu14 =`button -al "center"  -w $ButtonW -h $ButtonH -l "下载预设"  -c J_nClothTool_downLoadPresets`;
                string $cbu15 =`button -al "center"  -w $ButtonW -h $ButtonH -l "上传预设"  -c J_nClothTool_upLoadPresets`;
    /////////////////////////////////////////////////////////编辑窗体   
    formLayout -e 
        -af $cbu01 "top" 1  -ap $cbu01 "left" 0 1    -ap $cbu01 "right" 0 24
        -af $cbu02 "top" 1  -ap $cbu02 "left" 0 25   -ap $cbu02 "right" 0 49
        -af $cbu03 "top" 1  -ap $cbu03 "left" 0 50   -ap $cbu03 "right" 0 74
        -af $cbu04 "top" 1  -ap $cbu04 "left" 0 75   -ap $cbu04 "right" 0 99
        
        -ac $cbu05 "top" 1 $cbu01 -ap $cbu05 "left" 0 1    -ap $cbu05 "right" 0 24
        -ac $cbu06 "top" 1 $cbu01 -ap $cbu06 "left" 0 25    -ap $cbu06 "right" 0 49
        -ac $cbu07 "top" 1 $cbu01 -ap $cbu07 "left" 0 50    -ap $cbu07 "right" 0 74
        -ac $cbu08 "top" 1 $cbu01 -ap $cbu08 "left" 0 75    -ap $cbu08 "right" 0 99
        
        J_clothFormLayout;
    formLayout -e 
        //-af outPutTextField "top" 1 -ap outPutTextField "left" 0 1 -ap outPutTextField "right" 0 85 
        //-af browsBtn "top" 1  -ac browsBtn "left" 5 outPutTextField   -ap browsBtn "right" 0 98 
        -af $cbu09 "top" 1  -ap $cbu09 "left" 0 1    -ap $cbu09 "right" 0 49
        -af $cbu10 "top" 1  -ap $cbu10 "left" 0 50    -ap $cbu10 "right" 0 99

        J_clothInOutFormLayout;
    formLayout -e 
        -af nclothPresetslist "top" 1  -ap nclothPresetslist "left" 0 1    -ap nclothPresetslist "right" 0 49
        -af nclothNodeslist "top" 1  -ap nclothNodeslist "left" 0 50    -ap nclothNodeslist "right" 0 99
        -ac $cbu11 "top" 1 nclothPresetslist -ap $cbu11 "left" 0 1    -ap $cbu11 "right" 0 20
        -ac $cbu12 "top" 1 nclothPresetslist -ap $cbu12 "left" 0 21    -ap $cbu12 "right" 0 40
        -ac $cbu13 "top" 1 nclothPresetslist -ap $cbu13 "left" 0 41    -ap $cbu13 "right" 0 60
        -ac $cbu14 "top" 1 nclothPresetslist -ap $cbu14 "left" 0 61    -ap $cbu14 "right" 0 80
        -ac $cbu15 "top" 1 nclothPresetslist -ap $cbu15 "left" 0 81    -ap $cbu15 "right" 0 99
        
        J_clothPresetsFormLayout;
showWindow J_nClothTool;
    J_nClothToolInit();
    J_nClothTool_runScriptJob();
}
  
//初始化界面
global proc J_nClothToolInit()
{
    string $J_nclothNodes[]=`ls -type nCloth`;
    textScrollList -e -ra nclothNodeslist;
    for($i in $J_nclothNodes)
    {textScrollList -e -a $i nclothNodeslist;}
    J_nClothTool_getPresets("ncloth","nclothPresetslist");

    //加载笔刷
    string $temp0=`getenv MAYA_LOCATION`;
    $temp0="source \""+$temp0+"/scripts/others/setNClothMapType.mel\"";
    string $temp1=`getenv MAYA_LOCATION`;
    $temp1="source \""+$temp1+"/scripts/others/artAttrNClothToolScript.mel\"";

    eval($temp0);eval($temp1);
}
/////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  创建布料 
global proc J_createNCloth()
{
    string $rn_obj[]= `ls -sl`;
    if (size($rn_obj)<1)
        return;
    if (`objectType $rn_obj[0]`!="transform"&&`objectType $rn_obj[0]`!="mesh")
        {print "选择至少一个模型";return;}
    string $his_list[];
    string $get_res[];
    for($obj_nc in $rn_obj)
        {
        select $obj_nc;
        string $nClothNode[]=`createNCloth 0`;  
        string $tr[]=`listRelatives -p $nClothNode[0]`;  
        rename $tr[0] ($obj_nc+"_nClo");
        $get_res= `listHistory $obj_nc`;
        $his_list=`listRelatives -p $get_res[1]`;
        rename  $his_list[0] ($obj_nc+"_cloth");
        rename  $get_res[0] ($obj_nc+"_outcloth");            
        }
}   


///读取本地预设,并刷新预设列表
///\param string $selectedType 所选节点类型
///\param string $uiTextScrollList 
global proc J_nClothTool_getPresets(string $selectedType,string $uiTextScrollList)
    {
    string $J_ppath = `internalVar -userPrefDir`;
    $J_ppath = substitute( "/prefs", $J_ppath, "");
    $J_ppath+="presets/attrPresets/"+$selectedType +"/";

    string $J_presets[]=`getFileList -folder $J_ppath  -filespec "*.mel"`;
    textScrollList -e -ra $uiTextScrollList;
    for($i in $J_presets)
    {
        textScrollList -e -a $i $uiTextScrollList;
    }
    }
///保存预设到本地,并刷新预设列表
global proc J_nClothTool_savePresets()
{
    string $selTextScrollList[]=`textScrollList -q -si nclothNodeslist`;
    string $preName = substituteAllString($selTextScrollList[0],":", "_");
    if(size($selTextScrollList))
    {
    saveAttrPreset($selTextScrollList[0],($preName+"pre"),0);
    string $selectedType=`objectType $selTextScrollList[0]`;
    textScrollList -e -ra nclothPresetslist;
    J_nClothTool_getPresets($selectedType,"nclothPresetslist");
    }
}

////////////////////////////////////////////////////创建碰撞
//create passive collider
global proc J_createPassive()
    {
        string $pa_obj[]= `ls -sl`;
        string $nr_list[]= `makeCollideNCloth`;
        for ($i=0;$i<size($nr_list);$i++)
            {
                setAttr ($nr_list[$i]+".thickness") 0.006;
                string $par[]=`listRelatives  -p $nr_list[$i]`;
                rename  $par[0] ($pa_obj[$i]+"_nRig");
            }
    }
/////////////////////////////////////////   取消碰撞
global proc J_diableCollider()
    {
    string $temp0[]=`createNConstraint disableCollision 0`;
    string $temp1[]=`listRelatives -p $temp0[0]`;
    string $newName="disColl_"+$temp1[0]+"#";
    rename $temp1[0] $newName;
    } 
/////////////////////////////////////////   选择元素
global proc J_selectMember()
    {dynamicConstraintMembership "select";}
/////////////////////////////////////////   点面约束
global proc J_pointToSuf()
    {
    string $temp0[]=`createNConstraint pointToSurface 0`;
    string $temp1[]=`listRelatives -p $temp0[0]`;
    string $newName="pointToSur_#";
    rename $temp1[0] $newName;
    } 
/////////////////////////////////////////   元素约束
global proc J_pointTopoint()
    {
    string $temp0[]=`createNConstraint pointToPoint 0`;
    string $temp1[]=`listRelatives -p $temp0[0]`;
    string $newName="pointTop_"+$temp1[0];
    rename $temp1[0] $newName;
    } 
global proc J_paintInputAttract()
{
setNClothMapType("inputAttract","",1); artAttrNClothToolScript 3 inputAttract;
}
global proc J_paintCollideStrength()
{setNClothMapType("collideStrength","",1); artAttrNClothToolScript 3 collideStrength;}
global proc J_inOutClothInfoPath()////////////////////////////////////////设定路径
{
    string $filePath[]=`fileDialog2 -fileMode 2 `;
    textField -e -tx $filePath[0]   outPutTextField;
}
//选择节点
global proc  J_nClothTool_selectitem()
{
    string $nn[]=`textScrollList -q -si nclothNodeslist`;
    select $nn;
}
///根据场景中的选择,选择列表中的节点
global proc J_nClothTool_selectNode()
{
    textScrollList -e -da  nclothNodeslist;
    string $sel[]=`ls -sl`;
    if (size($sel)<1)
        return;
    J_nClothToolInit();
    string $selNc[];
    int $count=0;
    for ($kk in $sel)
    {   string $temp[]=`listRelatives -c -shapes -type nCloth $kk`;
        if (size($temp))
        {$selNc[$count]=$temp[0];$count++;}
    }
    if (`objectType $sel[0]`=="nCloth")
        $selNc=$sel;
    string $list[]=`textScrollList -q -ai nclothNodeslist`;
    for($i in $selNc)
    {
    if(stringArrayContains( $i, $list))
    textScrollList -e -si $i nclothNodeslist;
    }
}
//启动后台跟踪程序,同步插件中和场景中的选择对象
global proc  J_nClothTool_runScriptJob()
{
    int $ff = `scriptJob -e "SelectionChanged" J_nClothTool_selectNode `;
    string $ss = ("scriptJob -k "+ $ff);
    scriptJob -uid "J_nClothTool" $ss;
}
//下载预设
global proc J_nClothTool_downLoadPresets()
{
    string $nClothPresetsPath=J_getFilePath()+"mayaPresets/nCloth/";
    string $destnationPath=`internalVar -userPresetsDir`+"attrPresets/nCloth/";
    string $fileList[]=`getFileList  -folder $nClothPresetsPath -filespec "*.mel"`;
    sysFile -makeDir $destnationPath;
    for ($item in $fileList)
    {
    string $desFile=$destnationPath+$item;
    string $souFile=$nClothPresetsPath+$item;
    sysFile -copy $desFile $souFile;
    }
    J_nClothTool_getPresets("ncloth","nclothPresetslist");
}
//上传预设
global proc J_nClothTool_upLoadPresets()
{
    string $destnationPath=J_getFilePath()+"mayaPresets/nCloth/";
    string $nClothPresetsPath=`internalVar -userPresetsDir`+"attrPresets/nCloth/";
    string $J_selPresets[]=`textScrollList -q -si nclothPresetslist`;
    
    sysFile -makeDir $destnationPath;
    for ($item in $J_selPresets)
    {
    string $desFile=$destnationPath+$item;
    string $souFile=$nClothPresetsPath+$item;
    sysFile -copy $desFile $souFile;
    }
    
}
///读取选择的预设,并将参数赋予选择的节点
global proc J_nClothTool_readPresets()
{
    string $J_selNode[]=`textScrollList -q -si nclothNodeslist`;
    string $J_selPresets[]=`textScrollList -q -si nclothPresetslist`;
    for($i in $J_selNode)
    {
        string $selectedType=`objectType $i`;
        string $J_ppath = `internalVar -userPresetsDir`+"attrPresets/nCloth/"+$J_selPresets[0];
        applyAttrPreset $i $J_ppath 1;//读取预设
    }
}
///删除选择的预设
global proc J_nClothTool_delPresets()
{   string $J_selNode[]=`textScrollList -q -ai nclothNodeslist`;
    string $J_selPresets[]=`textScrollList -q -si nclothPresetslist`;
        string $selectedType="nCloth";
        string $J_ppath = `internalVar -userPrefDir`;
        $J_ppath = substitute( "/prefs", $J_ppath, "");
        $J_ppath+="presets/attrPresets/"+$selectedType+"/" + $J_selPresets[0];
        sysFile -del  $J_ppath;//删除垃圾
        textScrollList -e -ri $J_selPresets[0] nclothPresetslist;
}






global proc J_importNcloth()
{
    python("JpyModules.vfx.J_nClothTool.J_importNcloth()");
}
global proc J_exportNcloth()
{
    python("JpyModules.vfx.J_nClothTool.J_exportNcloth()");
}
