/////////////////////////////////////////////////////////////
///\file J_livingQueue.mel
///
///\brief    批量解算
///
///\author 桔
///\version 1.0
///\date  15:23 2021/1/13
///History:  
///
///////////////////////////////////////////////////////////////
global proc J_livingQueue()//主程序
{
    /////////////////////////////////////////////////////////////生成窗体
    string $checkBoxs[];
    if (`window -ex J_LivingQueue`)
        deleteUI -window J_LivingQueue;
    window -w 300 -h 360 -title "J_Living" J_LivingQueue;
    string $form = `formLayout`;
    textField -text "id" J_livingQueueId;
    textField -text "ip" J_livingQueueIp;
    textField -text "prot" J_livingQueuePort;
    textField -text "rate" J_livingQueueRate;
    textField  J_livingQueueNamePrefx;
    
    button -l "保存提交" -c J_livingQueue_submitJobs J_livingQueueSubmitJob;
    button -l "极速提交" -c J_livingQueue_fastSubmitJobs J_livingQueueFastSubmitJob;
    string $tabs = `tabLayout -innerMarginWidth 5 -innerMarginHeight 5  J_LivingQueueTableLayout`;
    formLayout -edit

        -af J_livingQueueId "top"   0
        -ap J_livingQueueId "left"  0 0
        -ap J_livingQueueId "right" 0 10
        
        -af J_livingQueueIp "top"    0
        -ap J_livingQueueIp "left"  0 10
        -ap J_livingQueueIp "right" 0 40
        
        -af J_livingQueuePort "top"    0
        -ap J_livingQueuePort "left"  0 40
        -ap J_livingQueuePort "right" 0 50
        
        -af J_livingQueueRate "top"    0
        -ap J_livingQueueRate "left"  0 50
        -ap J_livingQueueRate "right" 0 60
        
        -af J_livingQueueNamePrefx "top"    0
        -ap J_livingQueueNamePrefx "left"  0 60
        -ap J_livingQueueNamePrefx "right" 0 80
        
        -af J_livingQueueSubmitJob "top"    0
        -ap J_livingQueueSubmitJob "left"  0 80
        -ap J_livingQueueSubmitJob "right" 0 90
        
        -af J_livingQueueFastSubmitJob "top"    0
        -ap J_livingQueueFastSubmitJob "left"  0 90
        -ap J_livingQueueFastSubmitJob "right" 0 100
        
        -ac $tabs "top" 0 J_livingQueueId
        -af $tabs "left"   0
        -af $tabs "bottom" 0
        -af $tabs "right"  0
        $form ;
    showWindow J_LivingQueue;
    J_livingQueue_init($tabs);
}



global proc J_livingQueue_init(string $tabs)
{
//记录节点属性
string $hairAttrS[]={};
string $nclothAttrS[]={};
string $nucleusAttrS[]={};
string $nrigidAttrS[]={};
//查询所有选择节点的shape
    string $sel[]=`ls -l -sl -dagObjects`;
    string $selTypes[];
    int $count=0;
    //抽取所有选择的类型
    for ($i=0;$i<size($sel);$i++ )
    {
        if (`objectType $sel[$i]`!="transform")
        {
            $selTypes[$count]=`objectType $sel[$i]`;
            $count++;
        }
    }
    //去重
    $selTypes=stringArrayRemoveDuplicates($selTypes);
    //根据类型建立面板，最多不超过16页，只提取前16个类型
    for ($i=0;$i<16;$i++)
        {
        if ($i == size($selTypes)) break;
        string $nodeProperts[];
        string $nodePropertsTemp[];
        for ($item in $sel)
        {
        if (`objectType $item`==$selTypes[$i])
            {
            $nodePropertsTemp=`listAttr -se -s -k $item`;
            break;
            }
        }
        int $countTemp=0;
        for ($item in $nodePropertsTemp)
        {
            if (`gmatch $item "*.*"`)continue;
            $nodeProperts[$countTemp]=$item;
            $countTemp++;
        }

        string $J_LivingQueueFormLayout=`formLayout  -numberOfDivisions 100  `;
        string $nodeList=`textScrollList -sc J_livingQueue_passSelectioinValueToUi   -allowMultiSelection 1 -h 110`;
        for ($item in $sel )
        {
            if (`objectType $item`==$selTypes[$i])
            {
            textScrollList-e -a $item  $nodeList;
            }
        }
        string $scrollLayoutT=`scrollLayout`;
            for ($j=0;$j<size($nodeProperts);$j++)
            {
            rowColumnLayout -numberOfColumns 5  ;
                string $floatSliderGrpNode=`floatSliderGrp -label $nodeProperts[$j] -field true
                    -minValue 0 -maxValue 2
                    -fieldMinValue -1000 -fieldMaxValue 1000
                    -value 0`;
                floatSliderGrp -e -cc ("J_livingQueue_passSliderValueToNode(\""+$floatSliderGrpNode+"\")") $floatSliderGrpNode;
            setParent ..;
            }
        setParent ..;
        setParent ..;
        tabLayout -edit  -tabLabel $J_LivingQueueFormLayout $selTypes[$i] $tabs;
        
        /////////////////////////////////////////////////////////编辑窗体  
        formLayout -e 
            -ap $nodeList left 0 1
            -ap $nodeList top 0 1
            -ap $nodeList right 0 100
            -ap $nodeList bottom 0 40
            
            -ap $scrollLayoutT left 0 1
            -ap $scrollLayoutT top 1 40  
            -ap $scrollLayoutT right 0 100
            -ap $scrollLayoutT bottom 0 100

            $J_LivingQueueFormLayout;
        }
    //读取配置文件
    string $mypath=`internalVar -ups`+"J_livingQueueSettings.mel";
    $fileId=`fopen $mypath "r"`;
    string $nextLine = `fgetword $fileId  `;
    string $info[];
    int $count1=0;
    while ( size( $nextLine ) > 0 )
    {
        $info[$count1]=$nextLine;
        $nextLine = `fgetword $fileId`;
        $count1++;
    }
    fclose $fileId;
    textField -e -text $info[0] J_livingQueueId;
    textField -e -text $info[1] J_livingQueueIp;
    textField -e -text $info[2] J_livingQueuePort;
    textField -e -text $info[3] J_livingQueueRate;
    textField -e -text $info[4] J_livingQueueNamePrefx;
    
}
global proc J_livingQueue_passSelectioinValueToUi()
{
    string $formlayout=`tabLayout -q -ca -st J_LivingQueueTableLayout`;
    string $formLayoutChs[]=`formLayout -q -ca $formlayout`;
    string $selectedItemInList[]=`textScrollList -q -si $formLayoutChs[0]`;
    string $rowColumnLayouts[]=`scrollLayout -q -ca $formLayoutChs[1]`;
    for ($item in $rowColumnLayouts)
    {
        string $sliders[]=`rowColumnLayout -q -ca $item `;
        string $sliderLabelName=`floatSliderGrp -q -label $sliders[0]`;
        int $countOfSelectedItems=size($selectedItemInList);
        string $nodeName=$selectedItemInList[$countOfSelectedItems-1];
        if (`attributeExists $sliderLabelName $nodeName` )
        {
        float $value=`getAttr ($nodeName+"."+$sliderLabelName)`;
        floatSliderGrp -e -value $value $sliders[0];    
        floatSliderGrp -e -maxValue ($value*10) $sliders[0];          
        }
    }
    select $selectedItemInList;
}

global proc J_livingQueue_passSliderValueToNode(string $sliderName)
{   
    string $formlayout=`tabLayout -q -ca -st J_LivingQueueTableLayout`;
    string $formLayoutChs[]=`formLayout -q -ca $formlayout`;
    string $selectedItemInList[]=`textScrollList -q -si $formLayoutChs[0]`;
    for ($item in $selectedItemInList)
    {
        string $sliderLabelName=`floatSliderGrp -q -label $sliderName`;
        float $value=`floatSliderGrp -q -v $sliderName`;
        catch(`setAttr ($item+"."+$sliderLabelName) $value`);        
    }
}

global proc J_livingQueue_saveSetting()
{
    string $id=`textField -q -text J_livingQueueId`;
    string $ip=`textField -q -text J_livingQueueIp`;
    string $po=`textField -q -text J_livingQueuePort`;
    string $ra=`textField -q -text J_livingQueueRate`;
    string $pf=`textField -q -text J_livingQueueNamePrefx`;
    string $info=$id+" "+$ip+" "+$po+" "+$ra+" "+$pf+" ";
    string $mypath=`internalVar -ups`+"J_livingQueueSettings.mel";
    $fileId=`fopen $mypath "w"`;
    fwrite $fileId $info;
    fclose $fileId;
}
global proc J_livingQueue_submitJobs()
{
    string $filePath=`file -s`;
    string $id=`textField -q -text J_livingQueueId`;
    string $ip=`textField -q -text J_livingQueueIp`;
    string $po=`textField -q -text J_livingQueuePort`;
    string $ra=`textField -q -text J_livingQueueRate`;
    string $pf=`textField -q -text J_livingQueueNamePrefx`;
    string $ipPort=$ip+"&"+$po+"&"+$id+"&"+$ra;
    string $scriptToRun="";
    string $scName=`file -q -sceneName`;
    string $fileName=`file -q -sceneName -shortName`;
    string $buffer[];
    $numTokens = `tokenize $scName "/" $buffer`;
    string $path ="";
    for ($i=0;$i<(size($buffer)-1);$i++)
        $path+=$buffer[$i]+"/";
        
    $numTokens = `tokenize $fileName "." $buffer`;
    $fileName=$buffer[0];
    string $fullPathName=$path+$fileName+"_id_"+$id+"_"+$pf+"."+$buffer[1];
    $scriptToRun+="\\n";
    $scriptToRun+="file -rn \""+$fullPathName+"\";";
    $scriptToRun+="\\n";
    $scriptToRun+="file -save;" ;
    string $pyCommand="JpyModules.vfx.J_CFXWorkFlow.J_CFXWorkFlow_LivingSim("+"'"+$ipPort+"','"+$scriptToRun+"')";
    python($pyCommand);
    J_livingQueue_saveSetting();
}
global proc J_livingQueue_fastSubmitJobs()
{
    //ip prot
    string $filePath=`file -s`;
    string $id=`textField -q -text J_livingQueueId`;
    string $ip=`textField -q -text J_livingQueueIp`;
    string $po=`textField -q -text J_livingQueuePort`;
    string $ra=`textField -q -text J_livingQueueRate`;
    string $pf=`textField -q -text J_livingQueueNamePrefx`;
    string $ipPort=$ip+"&"+$po+"&"+$id+"&"+$ra;
    
    
    //保存节点属性
    //每个页面
    string $scriptToRun="";
    string $formlayout[]=`tabLayout -q -ca J_LivingQueueTableLayout`;
    for ($formItem in $formlayout)
    {
        string $formLayoutChs[]=`formLayout -q -ca $formItem`;
        string $scrolllListItems[]=`textScrollList -q -ai $formLayoutChs[0]`;
        string $scrolllListSelItems[]=`textScrollList -q -si $formLayoutChs[0]`;
        string $rowColumnLayouts[]=`scrollLayout -q -ca $formLayoutChs[1]`;
        for ($scrolllListItem in $scrolllListItems)
        {
            for ($item in $rowColumnLayouts)
            {
                string $sliders[]=`rowColumnLayout -q -ca $item `;
                string $sliderLabelName=`floatSliderGrp -q -label $sliders[0]`;

                if (`attributeExists $sliderLabelName $scrolllListItem` )
                {
                    float $value =`getAttr ($scrolllListItem+"."+$sliderLabelName)`;
                    $scriptToRun+="catch(`setAttr "+$scrolllListItem+"."+$sliderLabelName+" "+$value +"`);\n";
                }
            }
        }
    
    }
        
    string $scName=`file -q -sceneName`;
    string $fileName=`file -q -sceneName -shortName`;
    string $buffer[];
    $numTokens = `tokenize $scName "/" $buffer`;
    string $path ="";
    for ($i=0;$i<(size($buffer)-1);$i++)
        $path+=$buffer[$i]+"/";
        
    $numTokens = `tokenize $fileName "." $buffer`;
    $fileName=$buffer[0];
    string $fullPathName=$path+$fileName+"_id_"+$id+"_"+$pf+"."+$buffer[1];
    $scriptToRun+="\n";
    $scriptToRun+="file -rn \""+$fullPathName+"\";";
    $scriptToRun+="\n";
    $scriptToRun+="file -save;\n" ;
    
    $scriptToRun+="python(\"cmds.evalDeferred('JpyModules.vfx.J_CFXWorkFlow.J_CFXWorkFlow_CachePb("+$ra+"'jpg','',0,False,False,False)')\");\n";
    $scriptToRun+="python(\"cmds.evalDeferred('cmds.quit(force=True)')\");\n";
    
    string $scriptFile=$path+$fileName+"_id_"+$id+"_"+$pf+".mel";
    $fileId=`fopen $scriptFile "w"`;
    fwrite $fileId $scriptToRun;
    fclose $fileId;
    
    
    string $pyCommand="JpyModules.vfx.J_CFXWorkFlow.J_CFXWorkFlow_LivingSim("+"'"+$ipPort+"','"+$scriptFile+"')";
    python($pyCommand);
    J_livingQueue_saveSetting();
}





