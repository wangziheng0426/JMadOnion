/////////////////////////////////////////////////////////////
///\file J_runYetiCache.mel
///
///\brief    yeti出缓存
///
///\author 桔
///\version 1.0
///\date  15:23 2018/3/6
///History:  
///
///////////////////////////////////////////////////////////////

global proc J_runYetiCache()//yeti出缓存
{
//
//自动生产yeti缓存工具
/////////////////////////////////////////////////////////////窗口
string $checkBoxs[];
int $winWidth=300;
int $count=10;
if (`window -ex runYetiCacheWin`)
    deleteUI -window runYetiCacheWin;
    window -w 300 -h 600 -title "yeti批量缓存工具" runYetiCacheWin;
        int $ButtonW=80;
        int $ButtonH=30;
        frameLayout -cll 1 -cl 0 -h ($count*20) -l "yeti批量缓存"  myColum;
        formLayout  -numberOfDivisions 100  yetiCacheLayout;
        textScrollList  -sc "J_selectYetiNode" -allowMultiSelection 1 yetiList;
        radioButtonGrp -numberOfRadioButtons 2 -select 2
                -label "yeti版本" -labelArray2 "2.1.6之前版本"  "2.1.6及之后" rbg;

        separator  -w $winWidth -bgc .5 .5 .5 -st "in"  sepx1 ;
        textField  -w 260 -h 25 pathText;
        text -w 40 -h 25 -label "开始帧" startFrameInputText;
        textField  -w 40 -h 25 startFrameInputTextField;
        text -w 40 -h 25 -label "结束帧" endFrameInputText;
        textField  -w 40 -h 25 endFrameInputTextField;
        
        text -w 40 -h 25 -label "采样率" sampleInputText;
        textField  -text "1" -w 40 -h 25  sampleInputTextField;

        button -l "读取路径" -w $ButtonW  -h $ButtonH -c  "J_yetiGetPath" buPath ;
        button -l "为所有yeti建立缓存" -w $ButtonW  -h $ButtonH -c  "runYetiCacheAllBtn" buUp ;
        button -l "为选择的yeti建立缓存" -w $ButtonW  -h $ButtonH -c  "runYetiCacheBtn" buUpS ;
        button -l "关闭缓存"   -c  "J_runYetiCacheDisableCache" buDisableCache ;
        button -l "显示yeti节点"   -c  "J_runYetiCacheDisplayYetiMaya" buDisplayYetiMaya ;
        button -l "导入缓存"   -c  "J_runYetiCacheImportCache" buImportCache ;

        button -l "曲线转groom"   -c  "J_convertCurvesToYetiGroom" buConvertToGroom;
        button -l "重定向贴图"   -c  "" buReplaceTexture;
    /////////////////////////////////////////////////////////
    int $apx=5;
    int $apy=5;

    formLayout -e
        -ap yetiList left 0 1
        -af yetiList top 0
        -ap yetiList right 0 99
        -af yetiList bottom 300

        -af rbg left -15
        -ac rbg top $apy yetiList
        -af rbg right 0


        -af sepx1 left 0
        -ac sepx1 top $apy rbg
        -af sepx1 right 0

        -af pathText left $apx
        -ac pathText top $apy sepx1
        -af pathText right $apx

        -af startFrameInputText left $apx
        -ac startFrameInputText top $apy pathText
        -ap startFrameInputText right 0 15

        -ac startFrameInputTextField left $apx startFrameInputText
        -ac startFrameInputTextField top $apy pathText
        -ap startFrameInputTextField right 0 30

        -ac endFrameInputText left $apx startFrameInputTextField
        -ac endFrameInputText top $apy pathText
        -ap endFrameInputText right 0 45

        -ac endFrameInputTextField left $apx endFrameInputText
        -ac endFrameInputTextField top $apy pathText
        -ap endFrameInputTextField right 0 60

        -ac sampleInputText left $apx endFrameInputTextField
        -ac sampleInputText top $apy pathText
        -ap sampleInputText right 0 75

        -ac sampleInputTextField left $apx sampleInputText
        -ac sampleInputTextField top $apy pathText
        -ap sampleInputTextField right 0 99
        


        -af buPath left $apx
        -ac buPath top $apy startFrameInputText
        -ap buPath right 0 33

        -ac buUp left $apx buPath
        -ac buUp top $apy startFrameInputText
        -ap buUp right 0 66

        -ac buUpS left $apx buUp
        -ac buUpS top $apy startFrameInputText
        -ap buUpS right 0 99

        -af buDisableCache left $apx  
        -ac buDisableCache top $apy buUpS
        -ap buDisableCache right 0 99

        -af buDisplayYetiMaya left $apx  
        -ac buDisplayYetiMaya top $apy buDisableCache
        -ap buDisplayYetiMaya right 0 99

        -af buImportCache left $apx  
        -ac buImportCache top $apy buDisplayYetiMaya
        -ap buImportCache right 0 99

        -af buConvertToGroom left $apx  
        -ac buConvertToGroom top $apy buImportCache
        -ap buConvertToGroom right 0 99

        -af buReplaceTexture left $apx  
        -ac buReplaceTexture top $apy buConvertToGroom
        -ap buReplaceTexture right 0 99

        yetiCacheLayout;

showWindow runYetiCacheWin;
J_runYetiCacheInit();
J_runYetiCache_runScriptJob();
}


global proc J_yetiGetPath()//读取路径
{
    string $temp[]=`fileDialog2 -fm 3`;
    //textField -e -tx ($temp[0]+"/")  pathText;
    textField -e -tx ($temp[0])  pathText;
}
//初始化工具信息，读取yeti节点，设置输出路径
global proc J_runYetiCacheInit()
{
    string $allYetiMayaNodes[]=`ls -type pgYetiMaya`;
    for($i in $allYetiMayaNodes)
    {
        textScrollList -e -a $i yetiList;
    }
    string $filePath=`file -q -sn`;
    if($filePath!="")
    {
        string $buffer[];
        $numTokens = `tokenize $filePath "." $buffer`;
        string $cachePath=$buffer[0];
        for($j=1;$j<(size($buffer)-1);$j++)
        {
            $cachePath+="."+$buffer[$j];
        }
        string $buffer1[];
        $numTokens = `tokenize $buffer[size($buffer)-2] "/" $buffer1`;
        $cachePath+="_yetiCache";//输出路径
        textField  -e -text $cachePath pathText;
    }
        int $startFrame =`playbackOptions -q -min`;
        textField  -e -tx $startFrame startFrameInputTextField;

        int $endFrame  =`playbackOptions -q -max`;
        textField  -e -tx $endFrame endFrameInputTextField;
}

//输出列表中所选yeti节点缓存
global proc runYetiCacheBtn()
{
    python("JpyModules.vfx.J_yetiTool.J_yetiSaveCache()");
}


global proc runYetiCacheAllBtn()
{
    string $allYetiNode[]=`textScrollList -q -ai yetiList`;
    for ($i in  $allYetiNode)
    {textScrollList -e -si $i yetiList;}
    python("JpyModules.vfx.J_yetiTool.J_yetiSaveCache()");
}


global proc J_runYetiCacheImportCache()
{
    python("JpyModules.vfx.J_yetiTool.J_yetiLoadCache()");
}
global proc J_runYetiCacheDisableCache()
{
    string $sel[]=`ls -sl`;
    string $yetiNodes[]=`ls -sl -type pgYetiMaya`;
    if (size($sel)>0)
    {
        if (`objectType $sel[0]`=="transform")
        {$yetiNodes=`listRelatives -c -type pgYetiMaya`;}
    }
    if (size($yetiNodes)>0)
    {
        int $state=`getAttr ($yetiNodes[0]+".fileMode")`;
        $state=1-$state;
        for ($item in $yetiNodes)
        {
            setAttr ($item+".fileMode") $state;
            //setAttr -type "string" ($item+".cacheFileName") "";
        }
    }
}


global proc J_runYetiCacheDisplayYetiMaya()
{
    string $sel[]=`ls -sl`;
    string $yetiNodes[]=`ls -sl -type pgYetiMaya`;
    if (size($sel)>0)
    {
        if (`objectType $sel[0]`=="transform")
        {$yetiNodes=`listRelatives -c -type pgYetiMaya`;}
    }

    if (size($yetiNodes)>0)
    {
        int $state=`getAttr ($yetiNodes[0]+".displayOutput")`;
        $state=1-$state;
        for ($item in $yetiNodes)
        {
            setAttr ($item+".displayOutput") $state;
        }
    }
}

global proc J_selectYetiNode()
{
    string $yetiNodes[]=`textScrollList -q -si yetiList`;
    select $yetiNodes;
}
//转曲线为groom
global proc J_convertCurvesToYetiGroom()
{
    string $sel[]=`ls -sl`;
    select -d $sel[size($sel)-1];
    CreateSet;
    string $sets[]=`listConnections  -type "objectSet" $sel[0]`;
    string $shape[]=`listRelatives -c -s $sel[size($sel)-1]`;
    pgYetiConvertGuideSetToGroom($sets[0],$shape[0],0.1);

}
///启动后台跟踪程序,同步插件中和场景中的选择对象
global proc J_runYetiCache_runScriptJob()
{
    int $sjId = `scriptJob -e "SelectionChanged" J_runYetiCache_selectNode `;
    string $temp = ("scriptJob -k "+ $sjId);
    scriptJob -uid "runYetiCacheWin" $temp;

}
global proc J_runYetiCache_selectNode()
{
    
    textScrollList -e -deselectAll  yetiList;
    string $sel[]=`ls -sl`;
    string $listYeti[]=`textScrollList -q -ai yetiList`;
    
    
    //根据选择节点，选中列表节点
    for($i in $sel)
    {
    string $chTemp[]=`listRelatives -c $i`;
    if(stringArrayContains( $i, $listYeti))
        textScrollList -e -si $i yetiList;

    if(stringArrayContains( $chTemp[0], $listYeti))
        textScrollList -e -si $chTemp[0] yetiList;

    }
}