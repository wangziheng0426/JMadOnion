global proc Ju_attrTransfor()
{
	string $sel[]=`ls -sl`;
	if(size($sel))
	{
		Ju_runAttr($sel);
	}
	else
	{
	confirmDialog -title "错误" -message "啥也不选？你tm在逗我？" -button  "哦";
	}
}
global proc Ju_runAttr(string $sel[])
{
if (`window -ex Ju_attr`)
	deleteUI -window Ju_attr;
	window -w 300 -h 650 -title "预设批量传递" Ju_attr;
	formLayout  -nd 100  -h 295 Ju_attr_mainFormLayout;
	textScrollList -fn "boldLabelFont" -sc "Ju_selectitem" -allowMultiSelection true Ju_sourcesNodes;
	
	textScrollList -fn "boldLabelFont" -allowMultiSelection 0 Ju_sourcesPresets;


	string $button01=`button -l "保存预设" -c "Ju_savePresets"`;
	string $button02=`button -l "读取预设" -c "Ju_readPresets"`;
	string $button03=`button -l "删除预设" -c "Ju_delPresets"`;
	string $button04=`button -l "刷新窗口" -c "Ju_reFrashAttrTransformWin"`;
	checkBox -label "忽略变换节点" -v 1 Ju_ignoreTransform;
	

	formLayout  -e
		-af Ju_sourcesNodes top 3
		-af Ju_sourcesNodes left 2
		-ap Ju_sourcesNodes right 0 49
		-ap Ju_sourcesNodes bottom 0 70

		-af Ju_sourcesPresets top 3
		-ac Ju_sourcesPresets left 2 Ju_sourcesNodes
		-ap Ju_sourcesPresets right 0 99
		-ap Ju_sourcesPresets bottom 0 70

		-ac Ju_ignoreTransform top 3 Ju_sourcesNodes
		-ap Ju_ignoreTransform left 2 40
		-af Ju_ignoreTransform right 2
		
		-ac $button01 top 3 Ju_ignoreTransform
		-af $button01 left 2
		-af $button01 right 2

		-ac $button02 top 3 $button01
		-af $button02 left 2
		-af $button02 right 2

		-ac $button03 top 3 $button02
		-af $button03 left 2
		-af $button03 right 2

		-ac $button04 top 3 $button03
		-af $button04 left 2
		-af $button04 right 2

	Ju_attr_mainFormLayout;


	Ju_readSelectedNode($sel);
	Ju_selectNode();
	Ju_delPresets();
	Ju_runScriptJob();
	
	showWindow Ju_attr;
}
global proc Ju_getPresets(string $selectedType,string $uiTextScrollList)
	{
	string $J_ppath = `internalVar -userPrefDir`;
	$J_ppath = substitute( "/prefs", $J_ppath, "");
	$J_ppath+="presets/attrPresets/"+$selectedType +"/";

	string $Ju_presets[]=`getFileList -folder $J_ppath  -filespec "*.mel"`;
	textScrollList -e -ra $uiTextScrollList;
	for($i in $Ju_presets)
	{
		textScrollList -e -a $i $uiTextScrollList;
	}
	}

global proc Ju_savePresets()
{
	string $selTextScrollList[]=`textScrollList -q -si Ju_sourcesNodes`;
	string $preName = substituteAllString($selTextScrollList[0],":", "_");
	if(size($selTextScrollList))
	{
	saveAttrPreset($selTextScrollList[0],($preName+"pre"),0);
	string $selectedType=`objectType $selTextScrollList[0]`;
	textScrollList -e -ra Ju_sourcesPresets;
	Ju_getPresets($selectedType,"Ju_sourcesPresets");
	}
}

global proc Ju_readPresets()
{
	string $Ju_selNode[]=`textScrollList -q -si Ju_sourcesNodes`;
	string $Ju_selPresets[]=`textScrollList -q -si Ju_sourcesPresets`;
	for($i in $Ju_selNode)
	{
		string $selectedType=`objectType $i`;
		string $J_ppath = `internalVar -userPrefDir`;
		$J_ppath = substitute( "/prefs", $J_ppath, "");
		$J_ppath+="presets/attrPresets/"+$selectedType+"/" + $Ju_selPresets[0];
			applyAttrPreset $i $J_ppath 1;//读取预设
	}

}

global proc Ju_delPresets()
{	string $Ju_selNode[]=`textScrollList -q -ai Ju_sourcesNodes`;
	string $Ju_selPresets[]=`textScrollList -q -si Ju_sourcesPresets`;
		string $selectedType=`objectType $Ju_selNode[0]`;
		string $J_ppath = `internalVar -userPrefDir`;
		$J_ppath = substitute( "/prefs", $J_ppath, "");
		$J_ppath+="presets/attrPresets/"+$selectedType+"/" + $Ju_selPresets[0];
		sysFile -del  $J_ppath;//删除垃圾
		textScrollList -e -ri $Ju_selPresets[0] Ju_sourcesPresets;
}

global proc Ju_readSelectedNode(string $sel[])
{
	textScrollList -e -ra Ju_sourcesNodes;
	int $Ju_ignoreTransform_state =`checkBox -q -v Ju_ignoreTransform`;
	string $selectedType=`objectType $sel[0]`;
	if($Ju_ignoreTransform_state==1&&$selectedType=="transform")
	{
		string $tempNodeType[]=`listRelatives -c $sel[0]`;
		$selectedType=`objectType $tempNodeType[0]`;
	}
	
	string $allselTypeNode[]=`ls -type $selectedType`;
	for($i in $allselTypeNode)
	{
		textScrollList -e -a $i Ju_sourcesNodes;
	}
	Ju_getPresets($selectedType,"Ju_sourcesPresets");
}
global proc Ju_selectitem()
{
	string $nn[]=`textScrollList -q -si Ju_sourcesNodes`;
	select $nn;
}
global proc Ju_selectNode()
{
    textScrollList -e -da  Ju_sourcesNodes;
	string $sel[]=`ls -sl`;
	string $list[]=`textScrollList -q -ai Ju_sourcesNodes`;
	for($i in $sel)
	{
	if(stringArrayContains( $i, $list))
	textScrollList -e -si $i Ju_sourcesNodes;
	}
}
global proc Ju_runScriptJob()
{
	int $ff = `scriptJob -e "SelectionChanged" Ju_selectNode `;
	string $ss = ("scriptJob -k "+ $ff);
	scriptJob -uid "Ju_attr" $ss;
}
global proc Ju_reFrashAttrTransformWin()
{
	string $sel[]=`ls -sl`;
	Ju_readSelectedNode($sel);

}