/////////////////////////////////////////////////////////////
///\file J_batchSimulation.mel
///
///\brief    后台解算脚本，挂载在mayabatch上，或者在maya种直接执行，即可完成自动解算
///
///\author 桔
///\version 1.0
///\date  22:08 2019/1/14
///History:  
///
///////////////////////////////////////////////////////////////

/// 自动解算
global proc J_batchSimulation()
{
    string $referenceNodes[]=`ls -type reference`;
    for ($i in $referenceNodes)
        {
        string $refFileName=`referenceQuery -filename  $i `;
        print $refFileName;
        //预留替换ref
        }
    string $sceneName=`file -q -sceneName`;
    string $buffer[];
    $numTokens = `tokenize $sceneName "/" $buffer`;
    string $cachePath="";
    for ($index=0;$index<size($buffer)-1;$index++)
    {
    $cachePath=$cachePath+$buffer[$index]+"/";    
    }
    int $startFrame=`playbackOptions -q -min`-50;
    int $endFrame=`playbackOptions -q -max`;
    string $allNucleus[]=`ls -type nucleus`;
    for ($item in $allNucleus)
    {
    catch(`setAttr ($item+".startFrame") $startFrame`);    
    catch(`setAttr ($item+".enable") 1`);  
    }
    select `ls -type nCloth`;
    select -tgl `ls -type hairSystem`;
    catch(`deleteCacheFile 2 { "delete", "" }`) ;
    doCreateNclothCache 5 { "3", $startFrame, $endFrame, "OneFile", "1", "","1","","0", "add", "0", "1", "1","0","1","mcx" } ;
    setAttr -type "string" defaultRenderGlobals.currentRenderer "mayaSoftware";
    setAttr "defaultRenderGlobals.animation" 1;
    setAttr "defaultRenderGlobals.animationRange" $startFrame;
    setAttr "defaultRenderGlobals.endFrame" $endFrame;
    J_createMatForCloth(1);
    file -save;
}
//添加随机材质11:12 2019/1/17
global proc J_createMatForCloth(int $run)
{
if($run==1)
    {
        string $allClothNodes[]=`ls -type "nCloth"`;
        int $index=0;
        for ($item in $allClothNodes)
        {
            string $mat=`shadingNode -asShader blinn`;
            sets -renderable true -noSurfaceShader true -empty -name ($mat+"SG");
            connectAttr -f ($mat+".outColor") ($mat+"SG"+".surfaceShader");
            float $temp[3];
            J_randomColorForMat($temp);
            setAttr ($mat+".color") -type double3 $temp[0] $temp[1] $temp[2] ;
            string $getClothMesh[]=`listConnections -s 0 -shapes 1 -type "mesh" $item`;
            for ($item1 in $getClothMesh)
            {
                defaultNavigation -source $mat -destination  ($item1+".instObjGroups[0]") -connectToExisting;
            }
        }
    }
}
global proc J_randomColorForMat(float $color3[])
{
    int $a=rand(3);
    $color3[0]=rand(.7);
    $color3[1]=rand(.7);
    $color3[2]=rand(.7);
    $color3[$a]=1;
}


