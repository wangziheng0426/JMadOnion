/////////////////////////////////////////////////////////////
///\file  J_createHairConstraint.mel
///\brief  创建经典毛发约束
///
///\author 桔
///\version 1.0
///\date   16:51 2018/1/16
///History:  
///
///////////////////////////////////////////////////////////////

global proc J_createHairConstraint()
{
    string $sel[]=`ls -sl`;
    string $J_hairNode="";
    if (`objectType $sel[0]`=="transform")
        {
            string $floS[]=`listRelatives -c -type "follicle" $sel[0]`;
            string $hairShape[]=`listConnections -type "hairSystem" $floS[0]`;
            $J_hairNode=$hairShape[0];
        }
    if (`objectType $sel[0]`=="follicle")
        {
            string $hairShape[]=`listConnections -type "hairSystem" $sel[0]`;
            $J_hairNode=$hairShape[0];
        }
    print $J_hairNode;
    J_createHairConstraintR(5,$J_hairNode);
}

global proc J_createHairConstraintR(int $mode,string $J_name)
{
	string $hsys[];
	int $ind[] = selectedHairSystemIndices( $hsys );
	int $numInd = size($ind);
	if( $numInd == 0 ){
		warning( (uiRes("m_createHairConstraint.kNoHairCurvesSelected")));
		return;
	} 
	int $numPinInputs = `getAttr -size ($hsys[0] + ".inputHairPin" )`;
	float $bb[];
	int $positionAtMidpoint = true;
	if( $positionAtMidpoint ){
		string $htform[] = `listTransforms $hsys[0]`;
		if( size($htform) > 0 ){
			select -d $htform[0];
		}
		$bb= `exactWorldBoundingBox`;
	}
	string $hpin = `createNode hairConstraint`;
    
	if( $positionAtMidpoint ){
		move (($bb[0] + $bb[3])*0.5) 
			 (($bb[1] + $bb[4])*0.5) 
			 (($bb[2] + $bb[5])*0.5) $hpin;
	}
	setAttr ($hpin + ".constraintMethod") ($mode+1);
	int $ci;
	for( $ci = 0; $ci < $numInd; $ci++ ){
		setAttr ($hpin + ".curveIndices[" + $ci + "]") ($ind[ $ci ]);
	}
	int $pinInd;
	for( $pinInd = 0; $pinInd <= $numPinInputs; $pinInd++ ){
		string $input = ($hsys[0] + ".inputHairPin[" + $pinInd +"]");
		if( $pinInd < $numPinInputs ){
			string $con = `connectionInfo -sfd $input`;
			if( size( $con ) > 0 ){
				// already connected, keep looking
				continue;
			}
		}
		connectAttr ($hpin + ".outPin[0]") $input;
		break;
	}
    string $parTemp[]=`listRelatives -p $hpin`;
    string $temps=`rename $parTemp[0] ($J_name+"_HC")`;
    select $temps;
}
