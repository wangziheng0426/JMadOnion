/////////////////////////////////////////////////////////////
///\file  J_runYetiCacheBatch.mel
///\brief  后台yeti跑缓存
///
///\author 桔
///\version 1.0
///\date   16:59 2018/1/16
///History:  
///
///////////////////////////////////////////////////////////////


///\param string $startFrame 开始时间
///\param string $endFrame 结束时间
///\return "success";
global proc string J_runYetiCacheBatch(int $samples)
{
	int $startFrame=`playbackOptions -q -min`;
	int $endFrame=`playbackOptions -q -max`;
	string $seletedYetiNode[]=`ls -type pgYetiMaya`;
	string $filePath=`file -q -sn`;
	string $yetiCachePath=J_getSceneFilePath()+"_yetiCache/";
	for ($yetiNode in $seletedYetiNode)
	{
		catch(J_runYetiCacheBatchEX($yetiNode,$yetiCachePath,$yetiNode,$startFrame,$endFrame,$samples));
	}
return "success";
}

///yeti逐个跑缓存
///\param string $yetiMayaNode 场景中的yeti节点
///\param string $yetiCachePath yeti缓存路径
///\param string $yetiFileName yeti缓存文件名
///\param string $yetiCacheStartTime 解算起始时间
///\param string $yetiCacheEndTime 解算结束时间
///\param string $yetiSamples 解算采样率
///\return "success";
global proc string J_runYetiCacheBatchEX(string $yetiMayaNode,string $yetiCachePath,string $yetiFileName,int $yetiCacheStartTime,int $yetiCacheEndTime,int $yetiSamples)
{
		if(!`file -q -ex $yetiCachePath`)
		{sysFile -makeDir $yetiCachePath;}
		print $yetiCachePath;
		select $yetiMayaNode;
		setAttr ($yetiMayaNode+".fileMode") 0;
		string $temp=$yetiCachePath+$yetiFileName+"_%04d.fur";
		pgYetiCommand -writeCache $temp -range $yetiCacheStartTime $yetiCacheEndTime -samples $yetiSamples   ;
		setAttr -type "string" ($yetiMayaNode+".cacheFileName") $temp;
		setAttr ($yetiMayaNode+".fileMode") 1;
return "success";
}