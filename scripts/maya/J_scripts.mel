/////////////////////////////////////////////////////////////
///\file J_scripts.mel
///
///\brief    加载工具栏菜单
///
///\author 桔
///\version 1.0
///\date  15:44 2018/1/9
///History:  
///
///////////////////////////////////////////////////////////////

global proc J_scripts(string $J_mainPath,string $J_menuLabel)
{
    ////////////////////////////////////////////////////////////////创建菜单
    string $J_menuName="madOrange";
    if (`menu -exists  $J_menuName`) deleteUI $J_menuName;
        {string $menu=`menu  -l $J_menuLabel -p MayaWindow  $J_menuName`;}  
    //读取菜单列表
    string $J_menuList=$J_mainPath+"/config/menuList.txt";
    //加载abc插件
    string $mayapath=`getenv MAYA_LOCATION`+"/bin/plug-ins/AbcExport.mll";
    loadPlugin( $mayapath); 
    int $J_fileId=`fopen $J_menuList "r"`;
    int $loopRun=1;
    fgetline $J_fileId;
    string $J_getWord=`fgetword $J_fileId`;
    print "----------------------------------------------------------------";
    print $J_getWord;
    print "\n";
    string $J_mainMenuName="";
    string $J_mainMenuLabel="";
    string $J_subInfo[];
    string $J_menuItems[];
    int $J_menuItemsCount=0;
    while ( $loopRun ) 
    {
        if($J_getWord=="<MainMenu>")
        {
            $J_getWord = `fgetword $J_fileId`;
            $J_mainMenuLabel=$J_getWord;
            $J_getWord = `fgetword $J_fileId`;
            $J_mainMenuName=$J_getWord ;
            $J_menuItems[$J_menuItemsCount]=`menuItem -p $J_menuName -subMenu 1 -label $J_mainMenuLabel -to 1 -aob 1 $J_mainMenuName`;
            $J_menuItemsCount++;
            $J_getWord = `fgetword $J_fileId`;
        }
        else{ $J_getWord = `fgetword $J_fileId`;}                   
        if($J_getWord=="<SubMenu>")
        {
            $J_getWord = `fgetword $J_fileId`;
            $J_subInfo[0]=$J_getWord;
            $J_getWord = `fgetword $J_fileId`;
            $J_subInfo[1]=$J_getWord ;
            $J_getWord = `fgetword $J_fileId`;
            $J_subInfo[2]=$J_getWord ;
                
            string $fileToSource="source \""+$J_mainPath+"/"+$J_subInfo[2]+"\"";
            catch(`eval $fileToSource `);
            menuItem -p $J_mainMenuName -label $J_subInfo[0] -c $J_subInfo[1] -to 1 ;
            $J_getWord = `fgetword $J_fileId`;
        }
        if($J_getWord==""){$loopRun =0;}
        
    }
    fclose $J_fileId;
    ////////////////////////////////////////////////////////////////创建菜单
    ////////////////////////////////////////////////////////////////读取mel命令
    string $JmodulesfilePath[];
    $JmodulesfilePath[0]=J_getSysPath()+"/Jmodules";
    J_getFolders($JmodulesfilePath,$JmodulesfilePath[0]);
    for ($JmodulesfilePathItem in $JmodulesfilePath)
    {
        string $J_modules[]=`getFileList  -folder $JmodulesfilePathItem -filespec "*.mel" `;
        for ($J_modelesItem in $J_modules)
        {
            catch(eval("source  \"" +$JmodulesfilePathItem+"/"+$J_modelesItem+"\""));
            print ("source  \"" +$JmodulesfilePathItem+"/"+$J_modelesItem+"\"");
            print "\n";
        }
    }
    ////////////////////////////////////////////////////////////////读取mel命令
    
    ////////////////////////////////////////////////////////////////读取py命令
    /*
    string $JmodulesfilePath[];
    $JmodulesfilePath[0]=J_getSysPath()+"/Jpymodules";
    J_getFolders($JmodulesfilePath,$JmodulesfilePath[0]);
    for ($JmodulesfilePathItem in $JmodulesfilePath)
    {
        string $J_modules[]=`getFileList  -folder $JmodulesfilePathItem -filespec "*.py" `;
        for ($J_modelesItem in $J_modules)
        {
            catch(eval("source  \"" +$JmodulesfilePathItem+"/"+$J_modelesItem+"\""));
            print ("source  \"" +$JmodulesfilePathItem+"/"+$J_modelesItem+"\"");
            print "\n";
        }
    }
    ////////////////////////////////////////////////////////////////读取py命令
    */
    menuItem -p $J_menuName -label "命令工具集" -c J_madOnionTools -aob 1 flying0; 
    
}
//递归找文件夹
global proc J_getFolders(string $resFolders[],string $inPutPath)
{
    string $JmodulesfilePath[]=` getFileList  -folder  $inPutPath`;
    $orgCount=size($resFolders);
    $count=0;
    for ($item in $JmodulesfilePath)
    {
        if (size(`getFileList  -folder  ($inPutPath+"/"+$item)`))
            {
                $resFolders[$count+$orgCount]=$inPutPath+"/"+$item;
                $count++;
                J_getFolders($resFolders,($inPutPath+"/"+$item));
            }
    }
}
//生成工具列表

global proc  J_madOnionTools()
{
/////////////////////////////////////////////////////////////生成窗体
string $Ju_mainPath=J_getSysPath();
string $Ju_commandList=$Ju_mainPath+"/config/commandList.txt";
int $Ju_fileId=`fopen $Ju_commandList "r"`;
int $loopRun=1;
    string $Ju_commandName[];
    string $Ju_commandLabel[];
    string $Ju_commandPath[];
    int $Ju_commandCount=0;
    int $Ju_winC=1;
    string $Ju_group[];
    int $Ju_groupCount=0;
    string $Ju_getWord=`fgetword $Ju_fileId`;
    
    string $Ju_frameLayout[];
    string $Ju_formLayout[];
    string $Ju_button[];
    string $Ju_separator[];
    /////////////////////////////////////////////////////////////////////主窗口
if (`window -ex J_MadOnionWin`)
    deleteUI -window J_MadOnionWin;
    window -w 245 -h 200 -title "J_MadOnion" J_MadOnionWin;
    frameLayout -w 240-cll 1 -cl 0  -l "工具集" xxForm;
    scrollLayout scrollLayout;
    formLayout  -numberOfDivisions 100  JuForm;
    while ( $loopRun ) 
    {
    if($Ju_getWord=="<label>")
        {
            $Ju_getWord = `fgetword $Ju_fileId`;
            $Ju_group[$Ju_groupCount]=$Ju_getWord;          
            $Ju_frameLayout[$Ju_groupCount]=`frameLayout -w 240 -cll 1 -cl 1  -l $Ju_group[$Ju_groupCount] -p JuForm`;
            $Ju_formLayout[$Ju_groupCount]=`formLayout  -numberOfDivisions 100 -p $Ju_frameLayout[$Ju_groupCount]`;
            $Ju_getWord = `fgetword $Ju_fileId`;
            if($Ju_groupCount==0)
            {
            formLayout  -e 
                -ap $Ju_frameLayout[$Ju_groupCount] left 0 1
                -af $Ju_frameLayout[$Ju_groupCount] top 0
                -ap $Ju_frameLayout[$Ju_groupCount] right 0 99
            JuForm;
            }
            else
            {
            formLayout  -e 
                -ap $Ju_frameLayout[$Ju_groupCount] left 0 1
                -ac $Ju_frameLayout[$Ju_groupCount] top 0 $Ju_frameLayout[$Ju_groupCount-1]
                -ap $Ju_frameLayout[$Ju_groupCount] right 0 99
            JuForm;
            }
            $Ju_groupCount++;
            $Ju_winC++;
            $Ju_commandCount=0;
        }
        if($Ju_getWord=="<command>")
        {
            $Ju_getWord = `fgetword $Ju_fileId`;
            $Ju_commandLabel[$Ju_commandCount]=$Ju_getWord;
            $Ju_getWord = `fgetword $Ju_fileId`;
            $Ju_commandName[$Ju_commandCount]=$Ju_getWord ;
            $Ju_getWord = `fgetword $Ju_fileId`;
            $Ju_commandPath[$Ju_commandCount]=$Ju_getWord ;
            $Ju_getWord = `fgetword $Ju_fileId`;            
            $Ju_button[$Ju_commandCount] =`button -l $Ju_commandLabel[$Ju_commandCount] -c  $Ju_commandName[$Ju_commandCount] -p $Ju_formLayout[$Ju_groupCount-1]`; 
            //if($Ju_commandCount%3==0){separator  -w 300 -bgc .5 .5 .5 -st "in"  -p $Ju_formLayout[$Ju_groupCount-1];}
            if($Ju_commandCount==0)
            {
            formLayout  -e 
                -ap $Ju_button[$Ju_commandCount] left 0 1
                -af $Ju_button[$Ju_commandCount] top 0
                -ap $Ju_button[$Ju_commandCount] right 0 99
            $Ju_formLayout[$Ju_groupCount-1];
            }else
            {
            formLayout  -e 
                -ap $Ju_button[$Ju_commandCount] left 0 1
                -ac $Ju_button[$Ju_commandCount] top 2 $Ju_button[$Ju_commandCount-1]
                -ap $Ju_button[$Ju_commandCount] right 0 99
            $Ju_formLayout[$Ju_groupCount-1];
            }
            $Ju_winC++;
            $Ju_commandCount++;
        }
        else{ $Ju_getWord = `fgetword $Ju_fileId`;} 
        
        if($Ju_getWord==""){$loopRun =0;}       
    }
        ////////////////////////////////////////////////////////////////读取命令
    fclose $Ju_fileId;
    int $windowHeightV=linstep(0,50,$Ju_winC)*1000;
    window -e -h $windowHeightV   J_MadOnionWin;
    print $windowHeightV;

    /////////////////////////////////////////////////////////编辑窗体   
showWindow J_MadOnionWin;

}