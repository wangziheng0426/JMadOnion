/////////////////////////////////////////////////////////////
///\file J_viewKeyTool.mel
///\brief 动画关键帧浏览工具
///
///\author 桔
///\version 1.0
///\date  18:45 2019/8/21
///History:  
///
///////////////////////////////////////////////////////////////

///判断窗口是否有物体被选择
///\return success
global proc  string J_viewKeyTool()
{
    string $sel[]=`ls -sl -ap`;
    if(size($sel))
    {
        J_viewKeyTool_runAttr($sel);
    }
    else
    {
    confirmDialog -title "错误" -message "啥也不选？？" -button  "哦";
    }
    return "success";
}

///打开主界面窗口
///\param string $sel 选择的节点
///\return success
global proc  string J_viewKeyTool_runAttr(string $sel[])
{
if (`window -ex J_viewKeyToolWin`)
    deleteUI -window J_viewKeyToolWin;
    window -w 300 -h 650 -title "动画关键帧浏览工具" J_viewKeyToolWin;
    formLayout  -nd 100  -h 295 J_viewKeyToolWin_mainFormLayout;
    string $treeControl = `treeView   -numberOfButtons 1 -abr false J_viewKeyTool_treeView`;
    treeView -edit -scc "selectCallBack" -editLabelCommand editLabelCommandCallBack $treeControl;

    string $button01=`button -l "读取控制器" -c "J_viewKeyTool_addItemToTree()"`;
    string $button02=`button -l "查看选中控制器关键帧" -c "J_viewKeyTool_openDopSheetWithSelection()"`;
    string $button03=`button -l "查看关键帧" -c "J_viewKeyTool_openDopSheet()"`;
    string $button04=`button -l "选择无关键帧控制器" -c "J_viewKeyTool_noKeyObject()"`;
    

    formLayout  -e
        -af $treeControl top 3
        -af $treeControl left 2
        -ap $treeControl right 0 99
        -ap $treeControl bottom 0 70

        
        -ac $button01 top 3 $treeControl
        -af $button01 left 2
        -af $button01 right 2

        -ac $button02 top 3 $button01
        -af $button02 left 2
        -af $button02 right 2

        -ac $button03 top 3 $button02
        -af $button03 left 2
        -af $button03 right 2

        -ac $button04 top 3 $button03
        -af $button04 left 2
        -af $button04 right 2

    J_viewKeyToolWin_mainFormLayout;
    showWindow J_viewKeyToolWin;
    J_viewKeyTool_runScriptJob;
    J_viewKeyTool_addItemToTree;
    return "success";
}


///\param string $sel 选择的节点
global proc J_viewKeyTool_addItemToTree()
{
    string $sel[]=`ls -sl`;
    treeView -e -ra J_viewKeyTool_treeView;
    string $characterRootCtrl =$sel[size($sel)-1];
    //treeView -e -addItem $characterRootCtrl "" J_viewKeyTool_treeView;
    string $itemList[]=$sel;//
    for($i in $itemList)
    {
        treeView -e -addItem $i "" J_viewKeyTool_treeView;
        string $newName="";
        print `attributeQuery -node $i  -exists  "J_viewKeyViewName"`;
        
        if (`attributeQuery -node $i  -exists  "J_viewKeyViewName"`)
        {
            $newName=`getAttr ($i+".J_viewKeyViewName")`;
        }
        if ($newName=="")
        {treeView -e  -displayLabel $i $i J_viewKeyTool_treeView;}
        else
        {treeView -e  -displayLabel $i $newName J_viewKeyTool_treeView;}
    }
    select -cl;
}
global proc J_viewKeyTool_sortItem(string $sel[],string $res[])
{


}
global proc  selectCallBack()
{
    string $list[]=`treeView -q -si J_viewKeyTool_treeView`;
    if (size($list)>0)
        {select $list;}
    else{select -cl;}
}
global proc string  editLabelCommandCallBack(string $itemName,string $newName)
{
    
    J_viewKeyTool_ctrlAttr($itemName,$newName);
    return $itemName;
    
}

global proc J_viewKeyTool_openDopSheet()
{
    DopeSheetEditor;
    toggleAutoLoad dopeSheetPanel1OutlineEd true;
    toggleAutoLoad dopeSheetPanel1OutlineEd false;
    string $allItem[]=`treeView -q -children "" J_viewKeyTool_treeView`;
    select $allItem;
    doReload dopeSheetPanel1OutlineEd;
    select -cl;
}
global proc J_viewKeyTool_openDopSheetWithSelection()
{
    DopeSheetEditor;
    toggleAutoLoad dopeSheetPanel1OutlineEd true;
    toggleAutoLoad dopeSheetPanel1OutlineEd false;
    string $allItem[]=`treeView -q -si   J_viewKeyTool_treeView`;
    select $allItem;
    doReload dopeSheetPanel1OutlineEd;
    select -cl;
}

global proc J_viewKeyTool_noKeyObject()
{
    select -cl;
    string $allItem[]=`treeView -q -children "" J_viewKeyTool_treeView`;
    for ($item in $allItem)
    {
        
        int $mark=0;
        string $conns[]=`listConnections -d 1 $item`;
        for ($item1 in $conns)
        {
            if (objectType($item1)=="animCurveTU"||objectType($item1)=="animCurveTL"||objectType($item1)=="animCurveTA")
            {
                $mark=1;
                break;
            }
        }
        if($mark==0)
            {treeView -edit -selectItem $item true J_viewKeyTool_treeView;select -tgl $item;}
    }
}

global proc  J_viewKeyTool_runScriptJob()
{
    int $ff = `scriptJob -e "SelectionChanged" J_viewKeyTool_selectNode `;
    string $ss = ("scriptJob -k "+ $ff);
    scriptJob -uid "J_viewKeyToolWin" $ss;
}
///根据场景中的选择,选择列表中的节点
global proc J_viewKeyTool_selectNode()
{
    treeView -e -clearSelection J_viewKeyTool_treeView;
    string $sel[]=`ls -sl`;
    string $list[]=`treeView -q -children "" J_viewKeyTool_treeView`;
    for($i in $sel)
    {
    if(stringArrayContains( $i, $list))
    treeView -e -si $i true J_viewKeyTool_treeView;
    }
}
global proc J_viewKeyTool_ctrlAttr(string $object,string $viewName)
{
    if (!`attributeQuery -node $object  -exists  "J_viewKeyViewName"`)
    {
        addAttr -longName "J_viewKeyViewName" -dt "string" $object;
    }
    if (`attributeQuery -node $object  -exists  "J_viewKeyViewName"`)
    {
        setAttr -type "string" ($object+".J_viewKeyViewName") $viewName;
    }
}
global proc J_viewKeyTool_onOffShowObj()
{
    string $allItem[]=`treeView -q -si   J_viewKeyTool_treeView`;
    for ($item in $allItem)
    {
        int $vis=`getAttr ($item+".visibility")`;
        setAttr ($item+".visibility") (abs(1-$vis));
    }
}

J_viewKeyTool()